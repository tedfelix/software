1 ' Copyright 2017, Ted Felix
2 ' File date: 3/18/1985 04:40:48 PM
5 DEFINT W,B,T,C,S,X,Y,P,M
8 GOSUB 9000
9 ' Initialize
10 IF FL=0 THEN MX=50
15 DIM ZSUN(26)
20 DEF FNRAND(X)=INT(RND*X)+1
30 GOSUB 7000:CLS:SCREEN 1:COLOR 0,0
59 ' Set-up Variables
60 XS=40:YS=40
80 C=160:S(0)=C:S(1)=C:S(2)=C:S(3)=C
90 CO=1:M=0:XC=C:CHAM=70
120 Y4=130:Y3=135:Y2=145:Y1=180:Y0=200
129 ' Alternate road height
130 'y4=100:y3=110:y2=130:y1=160:y0=200
160 W4=2:W3=10:W2=20:W1=40:W0=80
170 BC=199:TC=189
199 ' Set-up sun
200 KEY OFF:CIRCLE(10,8),10,,1.57,4.712001:CIRCLE(10,8),10,,1.57,4.712001,1.8
210 PAINT(2,8):GET(0,0)-(20,16),ZSUN
220 CLS:XS=40:YS=40
230 PUT(XS,YS),ZSUN
240 LOCATE 1,17:PRINT"Driver"
250 LOCATE 2,14:PRINT"By: Ted Felix"
260 LINE(0,Y4-1)-(320,Y4-1)
990 'Compute and display
1000 SP(0)=S(0):SP(1)=S(1):SP(2)=S(2):SP(3)=S(3):CP=C:GOTO 2000
1010 LINE(CP+W4,Y4)-(SP(3)+W3,Y3),0
1020 LINE(CP-W4,Y4)-(SP(3)-W3,Y3),0
1030 LINE(SP(3)+W3,Y3)-(SP(2)+W2,Y2),0
1040 LINE(SP(3)-W3,Y3)-(SP(2)-W2,Y2),0
1050 LINE(SP(2)+W2,Y2)-(SP(1)+W1,Y1),0
1060 LINE(SP(2)-W2,Y2)-(SP(1)-W1,Y1),0
1070 LINE(SP(1)+W1,Y1)-(SP(0)+W0,Y0),0
1080 LINE(SP(1)-W1,Y1)-(SP(0)-W0,Y0),0
1090 S(0)=S(1):S(1)=S(2):S(2)=S(3):S(3)=C
1100 C=C+FNRAND(CHAM-1)-CHAM/2
1110 LINE(C+W4,Y4)-(S(3)+W3,Y3),CO
1120 LINE(C-W4,Y4)-(S(3)-W3,Y3),CO
1130 LINE(S(3)+W3,Y3)-(S(2)+W2,Y2),C1
1140 LINE(S(3)-W3,Y3)-(S(2)-W2,Y2),C1
1150 LINE(S(2)+W2,Y2)-(S(1)+W1,Y1),C2
1160 LINE(S(2)-W2,Y2)-(S(1)-W1,Y1),C2
1170 LINE(S(1)+W1,Y1)-(S(0)+W0,Y0),C3
1180 LINE(S(1)-W1,Y1)-(S(0)-W0,Y0),C3
1190 C3=C2:C2=C1:C1=CO:GOTO 1000
1990 'Car move
2000 X=STICK(0)-MX
2010 LOCATE 4,30:PRINT USING"##.##";M/100
2020 M=M+1:PA=XA:XM=X/4:XA=X/7
2030 IF M=100 THEN CO=3:CHAM=100
2040 IF M=300 THEN CO=2:CHAM=130
2050 IF M>500 THEN CO=CO+1:CHAM=150:IF CO>3 THEN CO=1
2060 S(0)=S(0)-XM:S(1)=S(1)-XM
2070 S(2)=S(2)-XM:S(3)=S(3)-XM
2080 C=C-XM
2089 ' Erase Car (Trapezoid)
2090 LINE (145,199)-(150+PA,189),0
2100 LINE (175,BC)-(170+PA,TC),0
2110 LINE (150+PA,TC)-(170+PA,TC),0
2120 IF XS>0 THEN IF XS<300 THEN PUT(XS,YS),ZSUN
2130 XS=XS-XM
2140 IF XS>0 THEN IF XS<300 THEN PUT(XS,YS),ZSUN
2149 ' Draw Car (Trapezoid)
2150 LINE(145,BC)-(175,BC),1
2160 LINE(145,BC)-(150+XA,TC),1
2170 LINE(175,BC)-(170+XA,TC),1
2180 LINE(150+XA,TC)-(170+XA,TC),1
2190 IF XC-15<S(0)-W0 THEN 3000
2200 IF XC+15>S(0)+W0 THEN 3000
2210 GOTO 1010
2999 ' Crash!
3000 FOR I=0 TO 15:COLOR I
3010 FOR DE=0 TO 100:NEXT:NEXT
3020 SCREEN 0:CLS:LOCATE 1,1,0
3030 PRINT"You travelled";
3040 COLOR 23,0:PRINT USING "##.##";M/100;
3050 COLOR 7,0:PRINT" miles!"
3070 GOTO 30
7000 INPUT"Would you like a random game";A$
7010 A$=LEFT$(A$,1):IF A$="Y" OR A$="y" THEN 7100 ELSE RETURN
7100 RANDOMIZE VAL(TIME$)+VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))
7110 RETURN
8999 ' Check for joystick model
9000 ON ERROR GOTO 9500
9010 OPEN"i",1,"\joystick\JOYSTICK.DAT"
9020 INPUT#1,U,D,R,L
9030 CLOSE#1:ON ERROR GOTO 0
9040 DX=R-L:MX=DX/2+L:FL=1:RETURN
9500 FL=0:RETURN
9600 ' Variables used in Driver:
9610 '  MX     - Middle of Joystick
9620 '* ZSUN   - Array holding sun
9630 '  XS,YS  - Position of sun
9640 '  Y0-Y4  - Road Y positions
9650 '  W0-W4  - Road widths
9660 '  CHAM   - Road curve factor
9670 '* S(0-3) - Array with road X
9680 '  C      - Top of road X
9690 '  CO     - Top of road color
9700 '  C1-C3  - Rest of road color
9710 '  CP     - Previous top of road X
9720 '* SP(x)  - Previous road X array
9730 '  M      - Distance travelled
9740 '  BC     - Bottom of car Y
9750 '  TC     - Top of car Y
9760 '  XC     - Car X (Middle)
9770 '  X      - Joystick input
9780 '  XA     - Car top X position
9790 '  PA     - Previous top X
9800 '  XM     - X movement of car
9810 '* - Array
