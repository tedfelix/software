1 ' Copyright 2017, Ted Felix
2 ' File date: 10/15/1982
5 KEY OFF:WIDTH 40:CLS:SCREEN 0,0,0
10 DEF FNRAND(X)=INT(RND*X)+1
12 DEF FNWPM(T,L)=L/5/(T/60)
17 DEF FNSECS(X$)=VAL(RIGHT$(X$,2))+VAL(MID$(X$,4,2))*60+VAL(X$)*3600
20 RANDOMIZE VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))+VAL(TIME$)
30 CLS:PRINT"TYPING GAME"
40 PRINT"By: Ted Felix
50 PRINT:PRINT"=== Menu ==="
60 PRINT"1) Letter test"
70 PRINT"2) ASDFJKL; (spc) Test"
80 PRINT"3) Keyboard test"
90 PRINT"4) Upper - Lowercase test"
140 PRINT"Press <ESC> To exit."
150 PRINT:PRINT"Press the number of your selection"
160 A$=INKEY$:IF A$="" THEN 160
165 IF A$=CHR$(27)THEN CLS:KEY ON:END
170 A=VAL(A$):IF A<1 OR A>4 THEN 160
180 ON A GOTO 1000,1200,
1000 CLS:PRINT"Letter test":GOSUB 9400
1010 GOSUB 9100:GOSUB 9000:START$=TIME$:FOR I=1 TO N
1020 A=FNRAND(26)+64:B$=CHR$(A):IF B$=C$ THEN 1020 ELSE C$=B$
1030 LOCATE 12,20,0:PRINT B$;
1040 A$=INKEY$:IF A$="" THEN 1040
1050 IF A$<>B$ AND A$<>CHR$(A+32)THEN 1040
1060 NEXT:STOPS$=TIME$:GOSUB 9200
1070 CLS
1080 PRINT"In";MIN;"minutes and";SEC;"seconds,"
1090 PRINT"you typed";N;"letters."
1100 PRINT:PRINT"That's about";INT(FNWPM(A,N));"words/minute."
1110 GOSUB 9300
1120 GOTO 1000
1200 CLS:PRINT"ASDF  JKL;":PRINT:PRINT" <space>"
1210 PRINT:PRINT"This test will test how well you"
1220 PRINT"can remember which keys are under your"
1230 PRINT"fingers."
1240 GOSUB 9400:GOSUB 9100:GOSUB 9000
1250 RESTORE:FOR I=1 TO 9:READ A(I):NEXT
1260 START$=TIME$:FOR I=1 TO N
1270 A=FNRAND(9):IF A=LAST THEN 1270 ELSE LAST=A:LOCATE 12,20
1280 IF A=9 THEN PRINT"< >" ELSE PRINT CHR$(A(A));"  ";
1290 A$=INKEY$:IF A$="" THEN 1290
1300 IF A$<>CHR$(A(A))AND A$<>CHR$(A(A)+32)THEN 1290
1310 NEXT:STOPS$=TIME$
1320 DATA 65,83,68,70,74,75,76,59,32
1330 GOSUB 9200:CLS
1340 PRINT"In";MIN;"minutes and";SEC;"seconds,":PRINT"you typed";N;"letters under your":PRINT"fingers."
1350 GOSUB 9300:GOTO 1240
8999 END
9000 LOCATE 1,1:PRINT"GET READY!!!":FOR I=0 TO 3000:NEXT:PRINT"GET SET!!!"
9010 FOR I=0 TO 1000:NEXT:PRINT"GO!!!":RETURN
9100 WIDTH 40:CLS:LOCATE 12,20:COLOR 23,0:PRINT"Û"
9110 COLOR 7,0:RETURN
9200 START=FNSECS(START$)
9210 STOPS=FNSECS(STOPS$)
9220 A=STOPS-START
9230 SEC=A MOD 60
9240 MIN=A\60
9250 RETURN
9300 PRINT:PRINT:PRINT"Press any key to play again."
9310 PRINT:PRINT"Press the ESC key to exit to menu."
9320 A$=INKEY$:IF A$="" THEN 9320
9330 IF A$=CHR$(27)THEN CLS:RETURN 30
9340 CLS:RETURN
9400 PRINT:INPUT"How many letters would you like to be   tested on";N
9410 CLS:RETURN

