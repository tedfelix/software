1 ' Copyright 2017, Ted Felix
2 ' File date: 6/17/1983
5 DIM SH(11)
10 CLS:SCREEN 1:KEY OFF
20 DEF FNRNDX=INT(RND*270)+25
30 DEF FNRNDY=INT(RND*150)+25
40 DEF FNRAND(X)=INT(RND*X)+1
50 S$=SPACE$(39)
100 LINE(5,0)-(0,10):LINE(5,0)-(10,10)
110 LINE(0,10)-(5,9):LINE(5,9)-(10,10)
120 GET(0,0)-(10,10),SH
500 CLS
1000 FOR I=1 TO 20
1010 CIRCLE(FNRNDX,FNRNDY),FNRAND(20)
1020 NEXT
1030 X=155:Y=189:PUT(155,189),SH
1100 LOCATE 1,1:LINE INPUT;"Move or Fire? ";A$
1110 A$=LEFT$(A$,1)
1120 IF A$="f"OR A$="F"THEN 2000
1199 ' Move
1200 LOCATE 1,1:LINE INPUT;"Direction in Degrees (0 right)? ";A$
1210 D=VAL(A$):IF D<=360 THEN 1250
1220 LOCATE 1,1:PRINT"Entry must be from 0-360        ";
1230 FOR I=1 TO 2000:NEXT:GOTO 1200
1250 LOCATE 1,1:PRINT S$;:LOCATE 1,1:LINE INPUT;"Force? ";A$
1260 F=VAL(A$):IF F<101 AND F>0 THEN 1300
1270 LOCATE 1,1:PRINT"Force must be 1-100";
1280 FOR I=1 TO 2000:NEXT:LOCATE 1,1
1290 PRINT S$;:GOTO 1250
1300 XD=COS(D*1.745329E-02)
1310 YD=-SIN(D*1.745329E-02)
1320 F=F+(FNRAND(21)-11)
1330 PUT(X,Y),SH:FOR I=1 TO F:X=X+XD:Y=Y+YD
1340 IF POINT(X+5,Y)<>0 THEN 8000
1350 NEXT
1370 PUT(X,Y),SH
1490 GOTO 1100
1999 ' Fire
2000 LOCATE 1,1:LINE INPUT;"Direction in Degrees (0 right)? ";A$
2010 D=VAL(A$):IF D<=360 THEN 2050
2020 LOCATE 1,1:PRINT"Entry must be from 0-360        ";
2030 FOR I=1 TO 2000:NEXT:GOTO 2000
2050 LOCATE 1,1:PRINT S$;:LOCATE 1,1
2060 XD=COS(D*1.745329E-02)
2070 YD=-SIN(D*1.745329E-02)
2080 D=FNRAND(50):XF=X+5:YF=Y-1
2085 PUT(X,Y),SH
2090 FOR I=1 TO D:XF=XF+XD:YF=YF+YD
2100 LINE(XF-5,YF)-(XF+5,YF),0
2110 LINE(XF,YF)-(XF,YF+10),0
2120 NEXT
2130 PUT(X,Y),SH
2490 GOTO 1100
7999 ' Boom
8000 XC=X+5:YC=Y+5
8010 FOR C=1 TO 10:FOR I=1 TO 10:CIRCLE(XC,YC),I,2:NEXT
8020 FOR I=1 TO 10:CIRCLE(XC,YC),I,0:NEXT:NEXT
8030 LOCATE 1,1:PRINT S$;:LOCATE 1,1:PRINT"Boom!";
8040 GOTO 8040

