1 CLS:KEY OFF:SCREEN 0:WIDTH 40:PRINT"Blockade":PRINT"Player one uses the W,Z,A,S keys to     change directions.  Player two uses the arrow keys to change directions.        Whoever stays alive the longest, gets   the points."
2 PRINT:PRINT:INPUT"Press ENTER to play...";A$
3 ' Copyright 2017, Ted Felix
4 ' File date: 12/31/1986 02:20:34 PM
5  BSC=0:WSC=0
10 X=39:Y=24:YV=-1:XV=0
15 X1=2:Y1=1:Y1V=1:X1V=0
20 WIDTH 40:CLS:COLOR 7,0,1:LOCATE 1,1,0
30 DL=30:T=0
50 LOCATE 25,20:PRINT"B"BSC;TAB(30)"W"WSC;
1000 A$=INKEY$:R$=RIGHT$(A$,1)
1010 IF R$="H" THEN YV=-1:XV=0
1020 IF R$="P" THEN YV=1:XV=0
1030 IF R$="K" THEN XV=-1:YV=0
1040 IF R$="M" THEN XV=1:YV=0
1050 X=X+XV:Y=Y+YV:IF X<2 OR X>39 OR Y<2 OR Y>23 THEN 4000
1060 IF SCREEN(Y,X)<>32 THEN 4000
1070 LOCATE Y,X:PRINT CHR$(2);
2010 IF A$="w" THEN Y1V=-1:X1V=0
2020 IF A$="z" THEN Y1V=1:X1V=0
2030 IF A$="a" THEN X1V=-1:Y1V=0
2040 IF A$="s" THEN X1V=1:Y1V=0
2050 X1=X1+X1V:Y1=Y1+Y1V:IF X1<2 OR X1>39 OR Y1<2 OR Y1>23 THEN 3000
2060 IF SCREEN(Y1,X1)<>32 THEN 3000
2070 LOCATE Y1,X1:PRINT CHR$(1);
2075 FOR I=1 TO SP:NEXT
2076 T=T+1
2077 LOCATE 25,1:PRINT"Time:"T;
2080 GOTO 1000
3000 W=-1:FOR I=1 TO 50
3010 LOCATE Y1,X1:PRINT CHR$(1);:FOR DE=1 TO DL:NEXT
3020 LOCATE Y1,X1:PRINT CHR$(2);:FOR DE=1 TO DL:NEXT:NEXT
3030 GOTO 5000
4000 W=0:FOR I=1 TO 50
4010 LOCATE Y,X:PRINT CHR$(1);:FOR DE=1 TO DL:NEXT
4020 LOCATE Y,X:PRINT CHR$(2);:FOR DE=1 TO DL:NEXT:NEXT
5000 IF W THEN WSC=WSC+T ELSE BSC=BSC+T
5005 FOR I=1 TO 100:A$=INKEY$:NEXT
5010 GOTO 10
