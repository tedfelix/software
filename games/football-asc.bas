1 ' File date: 6/15/1983
10 '7/29/82:09:00pm
20 KEY OFF:WIDTH 80:SCREEN 0,0,0:DEF SEG:ON KEY(10) GOSUB 3040
30 FOR A=1 TO 9:ON KEY(A) GOSUB 1800:KEY(A) ON:NEXT
40 COLOR 3,0:CLS:POKE 106,0
50 LOCATE 1,1:PRINT STRING$(80,219)
60 FOR A=2 TO 22:LOCATE A,1:PRINT"л":LOCATE A,80:PRINT"л":NEXT
70 LOCATE 23,1:PRINT STRING$(80,219);
80 COLOR 15,0:LOCATE 2,30:PRINT"H E A D   C O A C H
90 LOCATE 5,22,O:PRINT"Would You Like Instructions ? <Y/N>":COLOR 3,0
100 RESP$=INKEY$:IF RESP$="" THEN 100
110 IF RESP$="n" OR RESP$="N" THEN 340
120 LOCATE 3,26,O:PRINT"Welcome to the FRIENDLY BOWL"
130 LOCATE 4,26:PRINT"----------------------------"
140 LOCATE 5,13,O:PRINT"You are about to match wits against the I.B.M. Personnal
150 LOCATE 6,11:PRINT"Computer in a game of Football. You will be playing  N F L
160 LOCATE 7,11:PRINT"standard rules for the most part. One of the major changes
170 LOCATE 8,11:PRINT"in the rules we will play by is that a quarter is composed
180 LOCATE 9,11:PRINT"of  30  plays as opposed to  15  minutes.   The Two Minute
190 LOCATE 10,11:PRINT"Warning will be given after  24  plays.
200 LOCATE 11,13:PRINT"At the beginning of the game,you will have the option of
210 LOCATE 12,11:PRINT"kicking or receiving.  If you opt to receive,  you will be
220 LOCATE 13,11:PRINT"given the ball on the 20 yard line. During the 1st and 3rd
230 LOCATE 14,11:PRINT"quarters, you will be moving form left to right and in the
240 LOCATE 15,11:PRINT"2nd  and  4th quarters, just the opposite.  You may choose
250 LOCATE 16,11:PRINT"1  of  7 different offensive plays. You may punt or try to
260 LOCATE 17,11:PRINT"kick a  field  goal on any down,  but take some advice and
270 LOCATE 18,11:PRINT"do not try from more than  45  yards  out.  If you fail to
280 LOCATE 19,11:PRINT"get a first down,  I will take possesion of the ball.  You
290 LOCATE 20,11:PRINT"may now select any  1  of  5 defensives to try and keep me
300 LOCATE 21,11:PRINT"from scoring.
310 COLOR 15,0:LOCATE 25,28,O:PRINT"Strike Any Key To Continue";:COLOR 3,0
320 IF INKEY$<>"" THEN 320
330 YES$=INKEY$:IF YES$="" THEN 330
340 CLS:XX=1:YY=1:GOSUB 3100
350 C$=CHR$(16)
360 VSR=0:HSR=0:DN=1:YDS=10:QTR=1:PLS=0:M$=CHR$(17):Y$=CHR$(16)
370 COLOR 15,0
380 FOR J=26 TO 55:LOCATE 1,J:PRINT"л":NEXT
390 FOR I=1 TO 8:LOCATE I,54:PRINT"лл":NEXT
400 FOR J=55 TO 26 STEP-1:LOCATE 8,J:PRINT"л":NEXT
410 FOR I=8 TO 1 STEP-1:LOCATE I,26:PRINT"лл":NEXT
420 COLOR 3,0
430 LOCATE 3,30:PRINT"HOME     QTR  VISITORS"
440 LOCATE 4,30:PRINT HSR;TAB(39);QTR;TAB(46);VSR
450 LOCATE 6,30:PRINT"DOWN":LOCATE 6,38:PRINT"YARDS TO GO"
460 LOCATE 6,34:PRINT DN
470 LOCATE 6,49:PRINT YDS
480 COLOR 2,0
490 FOR J=11 TO 69:LOCATE 12,J:PRINT"л":NEXT
500 FOR I=13 TO 22:FOR J=65 TO 69:LOCATE I,J:PRINT"А":NEXT J,I
510 FOR J=69 TO 11 STEP-1:LOCATE 23,J:PRINT"л":NEXT
520 FOR I=22 TO 13 STEP-1:FOR J=11 TO 15:LOCATE I,J:PRINT"А":NEXT J,I
530 COLOR 7,0
540 GOSUB 3420
550 FOR I=13 TO 22:FOR J=20 TO 60 STEP 5:LOCATE I,J:PRINT"н":NEXT J,I
560 COLOR 7,0
570 LOCATE 11,16:PRINT"0   10   20   30   40   50   40   30   20   10  0"
580 COLOR 7,0
590 FOR I=1 TO 10:FOR J=1 TO 5:READ YRD(I,J):NEXT J,I
600 COLOR 15,0:LOCATE 3,1:PRINT"Would You Like To":LOCATE 4,1:PRINT"Kick Or Receive? <K/R>":COLOR 3,0
610 IF INKEY$<>"" THEN 610
620 KR$=INKEY$:IF KR$="" THEN 620
630 IF KR$="R" OR KR$="r" THEN 660
640 IF KR$="K" OR KR$="k" THEN 660
650 GOTO 620
660 HOLD$=KR$
670 FOR AA=500 TO 150 STEP -5:SOUND AA,1:NEXT:SOUND AA,0
680 IF KR$="K" OR KR$="k" THEN OPS=55:NPS=55:GOTO 720
690 IF KR$="R" OR KR$="r" THEN OPS=25:NPS=25:GOTO 1190
700 GOTO 620
710 END
720 LOCATE 3,60:PRINT"    It's My Ball     ":LOCATE 4,60:PRINT"On The"
730 GOSUB 2780:COLOR 15,0:LOCATE 4,66:PRINT YLN
740 COLOR 3,0:LOCATE 4,70:PRINT"Yard Line "
750 COLOR 15,0:LOCATE 6,60:PRINT"Select A Defensive ":LOCATE 7,60:PRINT"Formation By Entering":LOCATE 8,60:PRINT"A Number From 1 To 5":COLOR 3,0
760 COLOR 15,0:LOCATE 2,31:PRINT"ЎЏ                 "
770 GOSUB 1810
780 DN=1:YDS=10
790 COLOR 14,0:IF QTR=1 OR QTR=3 THEN LOCATE 17,NPS:PRINT M$
800 IF QTR=2 OR QTR=4 THEN LOCATE 17,NPS:PRINT Y$
810 COLOR 15,0:LOCATE 6,34:PRINT DN:LOCATE 6,49:PRINT YDS:COLOR 3,0
820 GOSUB 2780:COLOR 15,0:LOCATE 4,66:PRINT YLN:COLOR 3,0
830 IF INKEY$<>"" THEN 830
840 P$=INKEY$:IF P$="" THEN 840
850 IF P$<"0" OR P$>"5" THEN 840
860 LOCATE 10,26:PRINT SPC(46)
870 POSI=VAL(P$)
880 GOSUB 1750
890 COLOR 7,0
900 IF OPS=20 OR OPS=25 OR OPS=30 OR OPS=35 OR OPS=40 OR OPS=45 OR OPS=50 OR OPS=55 OR OPS=60 THEN LOCATE 17,OPS:PRINT"н" ELSE LOCATE 17,OPS:PRINT"џ"
910 COLOR 3,0
920 PLS=PLS+1
930 IF PLS=25 AND (QTR=2 OR QTR=4) THEN GOSUB 3450
940 IF PLS>30 AND QTR=4 THEN GOTO 2920
950 IF PLS>30 AND QTR=2 THEN GOSUB 2870:GOTO 670
960 IF PLS>30 THEN GOSUB 2900:GOTO 790
970 IF DN>3 AND NPS<35 AND (QTR=1 OR QTR=3) THEN GOSUB 2620:GOTO 1190
980 IF DN>3 AND NPS>45 AND (QTR=2 OR QTR=4) THEN GOSUB 2620:GOTO 1190
990 IF DN>3 THEN GOSUB 2370:GOTO 1190
1000 IF YRD(RW,POSI)=0 OR YRD(RW,POSI)=98 OR YRD(RW,POSI)<10 THEN DELAY=20:ELSE DELAY=40
1010 COLOR 15,0:FOR HOLD=1 TO DELAY:SOUND 50,.5:LOCATE 10,33:PRINT"PLAY IN PROGRESS":LOCATE 10,33:PRINT"                ":SOUND 50,0:NEXT HOLD:COLOR 3,0
1020 IF YRD(RW,POSI)=99 THEN GOSUB 1970:GOTO 1190
1030 IF YRD(RW,POSI)=98 THEN GOSUB 2170:GOTO 1190
1040 IF YRD(RW,POSI)=100 THEN GOSUB 2250:GOTO 1190
1050 YDS=YDS-YRD(RW,POSI)
1060 IF YDS=< 0 THEN DN=1:YDS=10 ELSE DN=DN+1
1070 IF (QTR=1 OR QTR=3) THEN NPS=OPS-(YRD(RW,POSI)/2) ELSE NPS=OPS+(YRD(RW,POSI)/2)
1080 IF NPS<16 AND (QTR=1 OR QTR=3) THEN GOSUB 2250:GOTO 1190
1090 IF NPS<16 AND (QTR=2 OR QTR=4) THEN GOSUB 2200:GOTO 1190
1100 IF NPS>64 AND (QTR=2 OR QTR=4) THEN GOSUB 2250:GOTO 1190
1110 IF NPS>64 AND (QTR=1 OR QTR=3) THEN GOSUB 2200:GOTO 1190
1120 OPS=NPS
1130 IF QTR=1 OR QTR=3 THEN C$=M$
1140 IF QTR=2 OR QTR=4 THEN C$=Y$
1150 IF YRD(RW,POSI)=0 THEN GOSUB 2810
1160 IF YRD(RW,POSI)>0 THEN GOSUB 2830
1170 IF YRD(RW,POSI)<0 THEN GOSUB 2850
1180 GOTO 790
1190 GOSUB 2780: LOCATE 3,60:PRINT"  It's Your Ball":LOCATE 4,60:PRINT"On The"
1200 COLOR 15,0:LOCATE 4,66:PRINT YLN
1210 COLOR 3,0:LOCATE 4,70:PRINT"Yard Line "
1220 COLOR 15,0:LOCATE 6,60:PRINT"Select An Offensive":LOCATE 7,60:PRINT"Play By Entering A   ":LOCATE 8,60:PRINT"Number From 1 To 7  ":COLOR 3,0
1230 COLOR 15,0:LOCATE 2,31:PRINT"                ЎЏ"
1240 GOSUB 1660
1250 DN=1:YDS=10
1260 COLOR 14,0:IF QTR=1 OR QTR=3 THEN LOCATE 17,NPS:PRINT Y$
1270 IF QTR=2 OR QTR=4 THEN LOCATE 17,NPS:PRINT M$
1280 COLOR 15,0:LOCATE 6,34:PRINT DN:LOCATE 6,49:PRINT YDS
1290 GOSUB 2780:LOCATE 4,66:PRINT YLN:COLOR 3,0
1300 IF INKEY$<>"" THEN 1300
1310 P$=INKEY$:IF P$="" THEN 1310
1320 IF P$<"0" OR P$>"7" THEN 1310
1330 LOCATE 10,26:PRINT SPC(46)
1340 POSI=VAL(P$)
1350 GOSUB 1750
1360 COLOR 7,0
1370 IF OPS=20 OR OPS=25 OR OPS=30 OR OPS=35 OR OPS=40 OR OPS=45 OR OPS=50 OR OPS=55 OR OPS=60 THEN LOCATE 17,OPS:PRINT"н" ELSE LOCATE 17,OPS:PRINT"џ"
1380 COLOR 3,0
1390 PLS=PLS+1
1400 IF PLS=25 AND (QTR=2 OR QTR=4) THEN GOSUB 3450
1410 IF PLS>30 AND QTR=4 THEN GOTO 2920
1420 IF PLS>30 AND QTR=2 THEN GOSUB 2870:GOTO 670
1430 IF PLS>30 THEN GOSUB 2900:GOTO 1260
1440 IF POSI=7 THEN GOSUB 2300:GOTO 720
1450 IF POSI=6 THEN GOSUB 2440:GOTO 720
1460 IF YRD(RW,POSI)=0 OR YRD(RW,POSI)=98 OR YRD(RW,POSI)<10 THEN DELAY=20:ELSE DELAY=40
1470 COLOR 15,0:FOR HOLD=1 TO DELAY:SOUND 50,.5:LOCATE 10,33:PRINT"PLAY IN PROGRESS":LOCATE 10,33:PRINT"                 ":SOUND 50,0:NEXT HOLD:COLOR 3,0
1480 IF YRD(RW,POSI)=99 THEN GOSUB 1900:GOTO 720
1490 IF YRD(RW,POSI)=98 THEN GOSUB 2040:GOTO 720
1500 IF YRD(RW,POSI)=100 THEN GOSUB 2120:GOTO 720
1510 YDS=YDS-YRD(RW,POSI)
1520 IF YDS=<0 THEN DN=1:YDS=10 ELSE DN=DN+1
1530 IF QTR=1 OR QTR=3 THEN NPS=OPS+(YRD(RW,POSI)/2) ELSE NPS=OPS-(YRD(RW,POSI)/2)
1540 IF (QTR=1 OR QTR=3) AND NPS>64 THEN GOSUB 2120:GOTO 720
1550 IF (QTR=2 OR QTR=4) AND NPS>64 THEN GOSUB 2070:GOTO 720
1560 IF (QTR=2 OR QTR=4) AND NPS<16 THEN GOSUB 2120:GOTO 720
1570 IF (QTR=1 OR QTR=3) AND NPS<16 THEN GOSUB 2070:GOTO 720
1580 IF DN>4 AND (QTR=1 OR QTR=3) THEN LOCATE 10,27:PRINT"Ball Turned Over On 4th Down":BEEP:DN=1:YDS=10:C$=M$:OPS=NPS:GOTO 720
1590 IF DN>4 AND (QTR=2 OR QTR=4) THEN LOCATE 10,27:PRINT"Ball Turned Over On 4th Down":BEEP:DN=1:YDS=10:C$=Y$:OPS=NPS:GOTO 720
1600 OPS=NPS
1610 IF QTR=1 OR QTR=3 THEN C$=Y$ ELSE C$=M$
1620 IF YRD(RW,POSI)=0 THEN GOSUB 2810
1630 IF YRD(RW,POSI)>0 THEN GOSUB 2830
1640 IF YRD(RW,POSI)<0 THEN GOSUB 2850
1650 GOTO 1260
1660 LOCATE 1,2:PRINT"    OFFENSIVE PLAY    ":PRINT"     SELECTION(1-7)     ":PRINT"   ------------------"
1670 PRINT"   1 = Line Plunge    "
1680 PRINT"   2 = End Run        "
1690 PRINT"   3 = Screen Pass    "
1700 PRINT"   4 = Short Pass      "
1710 PRINT"   5 = Long Bomb       "
1720 PRINT"   6 = Field Goal      "
1730 PRINT"   7 = Punt            "
1740 RETURN
1750 S$=RIGHT$(TIME$,2)
1760 N=VAL(S$)
1770 RANDOMIZE(N)
1780 R=RND*10
1790 RW=FIX(R)
1800 RETURN
1810 LOCATE 1,2:PRINT"DEFENSIVE FORMATIONS":PRINT"    SELECTION(1-5) ":PRINT" --------------------"
1820 PRINT"   1 = Goal Line      "
1830 PRINT"   2 = Short Run      "
1840 PRINT"   3 = Long Run       "
1850 PRINT"   4 = Short Pass     "
1860 PRINT"   5 = Long Pass      "
1870 PRINT"                        "
1880 PRINT"                        "
1890 RETURN
1900 LOCATE 10,30:PRINT"!!!! I Intercepted !!!!":DN=1:YDS=10
1910 IF QTR=1 OR QTR=3 THEN NPS=OPS+5:C$=M$
1920 IF QTR=2 OR QTR=4 THEN NPS=OPS-5:C$=Y$
1930 IF (QTR=1 OR QTR=3) AND NPS>64 THEN NPS=55
1940 IF (QTR=2 OR QTR=4) AND NPS<16 THEN NPS=25
1950 OPS=NPS
1960 RETURN
1970 LOCATE 10,30:PRINT"!!! You Intercepted !!!":DN=1:YDS=10
1980 IF QTR=1 OR QTR=3 THEN NPS=OPS-5:C$=Y$
1990 IF QTR=2 OR QTR=4 THEN NPS=OPS+5:C$=M$
2000 IF (QTR=1 OR QTR=3) AND NPS<16 THEN NPS=25
2010 IF (QTR=2 OR QTR=4) AND NPS>64 THEN NPS=55
2020 OPS=NPS
2030 RETURN
2040 LOCATE 10,27:PRINT"!!!! Sorry, You Fumbled !!!!":BEEP:DN=1:YDS=10
2050 IF QTR=1 OR QTR=3 THEN C$=M$ ELSE C$=Y$
2060 RETURN
2070 LOCATE 10,30:PRINT"!!!!   Safety    !!!!":DN=1:YDS=10
2080 IF QTR=1 OR QTR=3 THEN NPS=55:OPS=55:C$=M$ ELSE NPS=25:OPS=25:C$=Y$
2090 HSR=HSR+2
2100 LOCATE 4,30:PRINT HSR
2110 RETURN
2120 LOCATE 10,30:PRINT"!!!!  TOUCHDOWN  !!!!":DN=1:YDS=10:GOSUB 3130
2130 IF QTR=1 OR QTR=3 THEN NPS=55:OPS=55:C$=M$ ELSE NPS=25:OPS=25:C$=Y$
2140 VSR=VSR+7
2150 LOCATE 4,47:PRINT VSR
2160 RETURN
2170 LOCATE 10,28:PRINT"!!!! Oops , I Fumbled !!!!":BEEP:DN=1:YDS=10
2180 IF QTR=1 OR QTR=3 THEN C$=Y$ ELSE C$=M$
2190 RETURN
2200 LOCATE 10,30:PRINT"!!!!   Safety    !!!!":DN=1:YDS=10
2210 IF QTR=1 OR QTR=3 THEN NPS=25:OPS=25:C$=Y$ ELSE NPS=55:OPS=55:C$=M$
2220 VSR=VSR+2
2230 LOCATE 4,47:PRINT VSR
2240 RETURN
2250 LOCATE 10,30:PRINT"!!!!  TOUCHDOWN  !!!!":DN=1:YDS=10
2260 IF QTR=1 OR QTR=3 THEN NPS=25:OPS=25:C$=Y$ ELSE NPS=55:OPS=55:C$=M$
2270 HSR=HSR+7
2280 LOCATE 4,30:PRINT HSR
2290 RETURN
2300 FOR AA=500 TO 150 STEP -5:SOUND AA,1:NEXT:SOUND AA,0
2310 LOCATE 10,30:PRINT"!!!!  Good Punt  !!!!":DN=1:YDS=10
2320 IF QTR=1 OR QTR=3 THEN NPS=OPS+20:C$=M$ ELSE NPS=OPS-20:C$=Y$
2330 IF (QTR=1 OR QTR=3) AND NPS>64 THEN NPS=55
2340 IF (QTR=2 OR QTR=4) AND NPS<16 THEN NPS=25
2350 OPS=NPS
2360 RETURN
2370 FOR AA=500 TO 150 STEP -5:SOUND AA,1:NEXT:SOUND AA,0
2380 LOCATE 10,30:PRINT"!!!!  Good Punt  !!!!":DN=1:YDS=10
2390 IF QTR=1 OR QTR=3 THEN NPS=OPS-20:C$=Y$ ELSE NPS=OPS+20:C$=M$
2400 IF (QTR=1 OR QTR=3) AND NPS<16 THEN NPS=25
2410 IF (QTR=2 OR QTR=4) AND NPS>64 THEN NPS=55
2420 OPS=NPS
2430 RETURN
2440 IF QTR=2 OR QTR=4 GOTO 2520
2450 IF NPS>25 AND RW<9 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=M$:GOTO 2590
2460 IF NPS>30 AND RW<7 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=M$:GOTO 2590
2470 IF NPS>35 AND RW<5 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=M$:GOTO 2590
2480 IF NPS>38 AND RW<4 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=M$:GOTO 2590
2490 LOCATE 10,26:PRINT"!!!!  Field Goal Try Wide  !!!!"
2500 IF NPS>55 THEN NPS=55:OPS=55
2510 GOTO 2610
2520 IF NPS<25 AND RW<9 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=Y$:GOTO 2590
2530 IF NPS<30 AND RW<7 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=Y$:GOTO 2590
2540 IF NPS>35 AND RW<5 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=Y$:GOTO 2590
2550 IF NPS<38 AND RW<4 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:VSR=VSR+3:C$=Y$:GOTO 2590
2560 LOCATE 10,26: PRINT"!!!!  Field Goal Try Wide  !!!!"
2570 IF NPS<25 THEN NPS=25:OPS=25
2580 GOTO 2610
2590 GOSUB 3130:LOCATE 4,47:PRINT VSR
2600 IF QTR=1 OR QTR=3 THEN NPS=55:OPS=55 ELSE NPS=25:OPS=25
2610 RETURN
2620 IF QTR=2 OR QTR=4 THEN 2690
2630 IF NPS<25 AND RW<9 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=Y$:GOTO 2750
2640 IF NPS<30 AND RW<7 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=Y$:GOTO 2750
2650 IF NPS<35 AND RW<5 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=Y$:GOTO 2750
2660 LOCATE 10,26:PRINT"!!!!  Field Goal Try Wide  !!!!"
2670 IF NPS<25 THEN NPS=25:OPS=25
2680 GOTO 2770
2690 IF NPS>55 AND RW<9 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=M$:GOTO 2750
2700 IF NPS>50 AND RW<7 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=M$:GOTO 2750
2710 IF NPS<45 AND RW<5 THEN LOCATE 10,26:PRINT"!!!!  Field Goal Was Good  !!!!":DN=1:YDS=10:HSR=HSR+3:C$=M$:GOTO 2750
2720 LOCATE 10,26:PRINT"!!!!  Field Goal Try Wide  !!!!"
2730 IF NPS>55 THEN NPS=55:OPS=55
2740 GOTO 2770
2750 LOCATE 4,30:PRINT HSR
2760 IF QTR=1 OR QTR=3 THEN NPS=25:OPS=25 ELSE NPS=55:OPS=55
2770 RETURN
2780 YLN=(NPS-15)*2
2790 IF YLN>50 THEN YLN=100-YLN
2800 RETURN
2810 LOCATE 10,32:IF POSI=1 OR POSI=2 THEN PRINT"No Gain On The Play" ELSE PRINT"  Incomplete Pass"
2820 RETURN
2830 LOCATE 10,27:IF POSI=1 OR POSI=2 THEN PRINT"   Gain Of" YRD(RW,POSI) "On The Play" ELSE PRINT"Pass Completed For" YRD(RW,POSI) "Yards"
2840 RETURN
2850 LOCATE 10,27:IF POSI=1 OR POSI=2 THEN PRINT"   Loss Of" ABS(YRD(RW,POSI)) "On The Play" ELSE PRINT"Quarterback Sacked:Loss Of" ABS(YRD(RW,POSI))
2860 RETURN
2870 QTR=QTR+1:PLS=1:LOCATE 9,32:PRINT"                   ":LOCATE 3,60:PRINT"End Of The Half ":LOCATE 4,60:PRINT SPC(19):LOCATE 4,39:PRINT QTR:GOSUB 3190
2880 IF HOLD$="K" OR HOLD$="k" THEN KR$="R" ELSE KR$="K"
2890 RETURN
2900 QTR=QTR+1:PLS=0:LOCATE 3,60:PRINT"End Of The Quarter":LOCATE 4,39:PRINT QTR:GOSUB 3340:NPS=80-OPS:OPS=NPS
2910 RETURN
2920 CLS
2930 LOCATE 4,28:PRINT"Time Is Up; The Game Is Over"
2940 IF HSR>VSR THEN LOCATE 7,28:PRINT"You Lost By A Score Of" HSR "To" VSR:LOCATE 8,31:PRINT"Better Luck Next Time"
2950 IF VSR>HSR THEN LOCATE 7,28:PRINT"You Won By A Score Of" VSR "To" HSR:LOCATE 8,31:PRINT"Congratulations  !!!!"
2960 COLOR 15,0:LOCATE 10,25:PRINT"Would You Like To Play Again? <Y/N>":COLOR 3,0
2970 IF INKEY$<>"" THEN 2970
2980 ANS$=INKEY$:IF ANS$="" THEN 2980
2990 IF ANS$="y" OR ANS$="Y" THEN RESTORE:GOTO 340
3000 IF ANS$<>"n" AND ANS$<>"N" THEN 2980
3010 CLS:RUN"MENU
3020 DATA 0,2,14,10,0,2,98,0,8,40,8,4,8,4,99,-2,-4,0,99,0,6,10,0,6,50,00,6,12,0,0,4,-2,-8,18,0,0,16,-2,0,99,14,30,6,0,0,2,0,4,2,0
3030 DATA 0,2,14,10,0,2,98,6,8,40,8,4,8,4,99,-2,-4,0,99,0,6,10,0,6,50,00,6,12,0,0,4,-2,-8,18,0,0,16,-2,0,99,14,30,6,0,0,2,0,4,2,0
3040 KEY(10) OFF:XX=CSRLIN:YY=POS(0):LOCATE 25,1:PRINT SPC(79);:LOCATE 25,20
3050 COLOR 15,0:PRINT"Do You Wish To Leave This Game? <Y/N>";:COLOR 3,0
3060 IF INKEY$<>"" THEN 3060
3070 A$=INKEY$:IF A$="" THEN 3070
3080 IF A$="Y" OR A$="y" THEN 3010
3090 IF A$<>"N" AND A$<>"n" THEN 3070
3100 LOCATE 25,1:PRINT SPC(79);:LOCATE 25,25:COLOR 0,7
3110 PRINT" Strike <F10> To Leave This Game ";:COLOR 3,0:LOCATE XX,YY
3120 KEY(10) ON:RETURN
3130 PLAY"MB MN T220 O3"
3140 PLAY"O3 E2 D#4 E4 F8 F4 E8 F2"
3150 PLAY"O3 F8 F4 F8 E4 F4 G8 G4 F#8 G2"
3160 PLAY"O3 A4 O4 C4 O3 B4 A4 G4 E4 C4 D4"
3170 PLAY"O3 E8 G4 F8 E4 D4 C2. MF"
3180 RETURN
3190 PLAY"MN T240"
3200 PLAY"O1 C2 O0 B4. O1 C8 O0 A4 O1 C2 D4 E-4 E4 F4 F#4 G4 P4 O0G2"
3210 PLAY"O3 E4 P8 E8 E4 E4 E4 P8 E8 F4 F4 D8 C#8 MS D8 E8 D4 C4"
3220 PLAY"O3 ML C32 O2 B16. MS B8 A4 B4 G4 MNO3G4 P8 G8 G4 G4"
3230 PLAY"O3 G4 P8 G8 G#4 G#4 ML A8 MN G#8 MS A8 O4C8O3 MNB4 A4 G#2."
3240 PLAY"O3 G#4 G4 P8 G8 MS O4 C4 P8 O3 A8 G4 F#4 G4 E4 MN D4"
3250 PLAY"O2 G8 F#8 G8 F#8 G4 O3 D4 O2 G8 F#8 G8 F#8 G4 O3"
3260 PLAY"O3 G4 P8 G8 MS O4 C4 P8 O3 A8 G4 F#4 G4 E-4 MN D4"
3270 PLAY"O2 F#8 E8 F#8 E8 F#4 G4 P4 G4.F8 E2 A4.G8 O1 B2 A2"
3280 PLAY"O1 G2 O2 F2 E2 D4.E8 F4 A2 O3 C4 D2 C2 O2 E1 D2"
3290 PLAY"O3 G4.F8 E2 A4. G8 O2 B2 A2 G2 O3F2 E2 D4.E8 F4 A4 O4D4.C8"
3300 PLAY"O3 E4 G4 C4.E8 D1 C1"
3310 RETURN
3320 RETURN
3330 RETURN
3340 PLAY "MN T200"
3350 PLAY "O2 C8 F8 A8 O3 C4 O2A8 O3C4."
3360 FOR I=450  TO 300  STEP -10:SOUND I,.3:NEXT I :SOUND 32600,5
3370 PLAY "O2 C#8 F#8 A#8 O3 C#4 O2A#8 O3C#4."
3380 FOR I=475  TO 325  STEP -10:SOUND I,.3:NEXT I :SOUND 32600,5
3390 PLAY "O2 D8 G8 B8 O3 D4 O2B8 O3D4."
3400 FOR I=500  TO 350  STEP -10:SOUND I,.3:NEXT I :SOUND 32600,5
3410 RETURN
3420 PLAY "MB MN T180"
3430 PLAY "O2 C8.C16 F8 C8 F8 A8 F4 F8.F16 A8 F8 A8 O3C8 O2 A4 F8.A16 O3 C4  O2 A8.F16 C4 C8.C16 F4 F8.F16 F4 MF"
3440 RETURN
3450 LOCATE 9,32:COLOR 31,0:PRINT"Two Minute Warning";:BEEP:RETURN

