10 ' Copyright 2017, Ted Felix
11 ' File date: 7/6/1983
90 PLAY"mb
95 REM Breakout
100 DEFINT A-Y:CLS:SCREEN 1:COLOR 0,1
110 CIRCLE(3,3),2,3:PAINT STEP(0,0),3
120 DIM BALL(10):GET(0,0)-(5,5),BALL
130 LINE(0,0)-(319,199),2,BF:LINE(80,20)-(241,185),0,BF
140 FOR I=0 TO 7:FOR J=0 TO 3:LINE(82+20*I,48+J*12)-STEP(18,8),J/2+1,BF:NEXT:NEXT
150 NBALLS=6:NBRKS=32
160 PX=150:PXINC=0
170 LOCATE 2,2:PRINT"Balls left ";NBALLS:IF NBALLS=0 THEN LOCATE 15,13:PRINT"You Lost!!!!":END
180 BX=80:BY=100:BXINC=4:BYINC=4:PUT(BX,BY),BALL
190 A$=INKEY$:IF A$="c"THEN PXINC=7 ELSE IF A$="z"THEN PXINC=-7 ELSE IF A$="x"THEN PXINC=0
195 ZYC=ZYC+.01
197 BYINC=SGN(BYINC)*(4+ZYC)
200 OLDPX=PX:PX=PX+PXINC:IF PX<80 OR PX>221 THEN PX=OLDPX
210 LINE(OLDPX,181)-(OLDPX+20,181),0:LINE(PX,181)-(PX+20,181),3:OLDPX=PX
220 OLDBX=BX:BX=BX+BXINC:IF BX<80 OR BX>234 THEN BXINC=-BXINC:BX=BX+2*BXINC:SOUND 200,1
230 OLDBY=BY:BY=BY+BYINC:IF BY<24 THEN BYINC=-BYINC:BY=BY+2*BYINC:SOUND 200,1
240 IF BY>175 THEN IF BX<PX-5 OR BX>PX+20 THEN 280 ELSE BYINC=-BYINC:BY=BY+2*BYINC:BXINC=((BX-PX)/2-4)+ZXC
250 PUT(OLDBX,OLDBY),BALL:P=POINT(BX+2,BY+2):IF P=0 THEN 260 ELSE SOUND 2000,1:ZYC=(3-P)*3:PAINT(BX+2,BY+2),0:BYINC=-BYINC:BY=BY+2*BYINC:LOCATE 2,20:NBRKS=NBRKS-1:PRINT"Bricks left"NBRKS:IF NBRKS=0 THEN LOCATE 15,13:PRINT"You Won!!!":END
260 PUT(BX,BY),BALL
270 GOTO 190
280 NBALLS=NBALLS-1:PX=150:LINE(OLDPX,181)-(OLDPX+20,181),0
285 IF NBALLS<=0 THEN 170
290 LINE(PX,181)-(PX+20,181),3
295 FOR I=1 TO 100:A$=INKEY$:NEXT:A$=INPUT$(1)
300 PUT(OLDBX,OLDBY),BALL:GOTO 170
