1 ' Copyright 2017, Ted Felix
2 ' File date: 7/30/1982
130 KEY OFF:CLS:SCREEN 1:COLOR 0,0
140 DIM CUR%(20)
150 GOSUB 440
160 FUEL=250:GRAVITY=.25:TINC=.3
170 WHILE CY<180
180 TIME=TIME+TINC
190 PUT (CX-4,CY-4),CUR%,XOR
200 CX=CX+VX:CY=CY+VY
210 VX=VX+AX:VY=VY+AY+GRAVITY*TINC
220 AX=0:AY=0:VX=VX+(-SGN(VX)*.05*TINC*ABS(VX))
225 GOSUB 2000
230 PUT (CX-4,CY-4),CUR%,XOR
240 GOSUB 330
250 WEND
260 LOCATE 7,13:PRINT"Time ";TIME
270 LOCATE 9,13:IF CX>165 OR CX<155 THEN PRINT"Off target"
280 LOCATE 11,13
290 IF VY>2 THEN PRINT"Enormous ";
300 IF VY>1 THEN PRINT"Crash!!!"
310 LOCATE 1,1:END
330 AX=(STICK(0)-50)/200:AY=(STICK(1)-50)/200
340 FUEL=FUEL-ABS(AX)-ABS(AY)
350 IF FUEL<5 THEN SOUND 500,1
360 IF FUEL<0 THEN SOUND 100,1:AX=0:AY=0
420 RETURN
440 LINE(0,4)-(8,4),1:LINE(4,0)-(4,8),1:CIRCLE(4,4),4,1
450 GET(0,0)-(8,8),CUR%:CX=4:CY=4
460 LINE(0,180)-(319,185),,B
470 LINE(155,175)-(155,185),2
480 LINE(165,175)-(165,185),2
490 RETURN
2000 IF CX<4 THEN CX=4:VX=-VX
2010 IF CY<4 THEN CY=4:VY=-VY
2020 IF CX>310 THEN CX=310:VX=-VX
2030 RETURN

