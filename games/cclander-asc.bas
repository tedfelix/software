1 ' Copyright 2017, Ted Felix
2 ' File date: 7/29/1982
130 KEY OFF:CLS:SCREEN 1
140 DIM CUR%(20)
150 GOSUB 440
160 FUEL=25:GRAVITY=.2:TINC=.3
170 WHILE CY<180
180 TIME=TIME+TINC
190 IF CX>3.9 AND CY>3.9 THEN PUT (CX-4,CY-4),CUR%,XOR
200 CX=CX+VX:CY=CY+VY
210 VX=VX+AX:VY=VY+AY+GRAVITY*TINC
220 AX=0:AY=0:VX=VX+(-SGN(VX)*.05*TINC*ABS(VX))
230 IF CX>3.9 AND CY>3.9 THEN PUT (CX-4,CY-4),CUR%,XOR
240 GOSUB 330
250 WEND
260 LOCATE 7,13:PRINT"Time ";TIME
270 LOCATE 9,13:IF CX>165 OR CX<155 THEN PRINT"Off target"
280 LOCATE 11,13
290 IF VY>2 THEN PRINT"Enormous ";
300 IF VY>1 THEN PRINT"Crash!!!"
310 LOCATE 1,1:END
330 A$=INKEY$:IF A$="" THEN RETURN
340 IF LEN(A$)<>2 THEN RETURN ELSE A=ASC(RIGHT$(A$,1))
350 FUEL=FUEL-1*TINC
360 IF FUEL<=0 THEN SOUND 150,3:RETURN
370 IF FUEL<5 THEN SOUND 1100,1:SOUND 1400,2
380 IF A=72 THEN AY=AY-1*TINC:GOTO 330
390 IF A=80 THEN AY=AY+1*TINC:GOTO 330
400 IF A=75 THEN AX=AX-1*TINC:GOTO 330
410 IF A=77 THEN AX=AX+1*TINC:GOTO 330
420 GOTO 330
440 LINE(0,4)-(8,4):LINE(4,0)-(4,8):CIRCLE(4,4),4
450 GET(0,0)-(8,8),CUR%:CX=4:CY=4
460 LINE(0,180)-(319,185),,B
470 LINE(155,175)-(155,185)
480 LINE(165,175)-(165,185)
490 RETURN
