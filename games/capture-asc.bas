1 RANDOMIZE TIMER
2 ' Copyright 2017, Ted Felix
5 DEF FNRNDX(X)=INT(RND*38)+2
6 DEF FNRNDY(Y)=INT(RND*21)+2
10 CLS:WIDTH 40:SCREEN 0:LOCATE 1,1,0
20 DIF=50
200 CLS:COLOR 4:S$=STRING$(40,177):S1$=CHR$(177)
210 PRINT S$;:FOR I=1 TO 21:PRINT S1$;SPACE$(38);S1$;:NEXT
220 PRINT S$;:COLOR 2
300 FOR I=1 TO 200
310 Y=FNRNDY(0):X=FNRNDX(0)
320 LOCATE Y,X:PRINT"Û";
330 NEXT
400 EX(1)=FNRNDX(0):EY(1)=FNRNDY(0):IF SCREEN(EY(1),EX(1))<>32 THEN 400
410 EX(2)=FNRNDX(0):EY(2)=FNRNDY(0):IF SCREEN(EY(2),EX(2))<>32 THEN 410
420 X1=FNRNDX(0):Y1=FNRNDY(0):IF SCREEN(Y1,X1)<>32 THEN 420
500 COLOR 23,0:LOCATE EY(1),EX(1):PRINT"*";
510 LOCATE EY(2),EX(2):PRINT"*";
520 FOR I=1 TO 2000:NEXT
530 COLOR 7,0:LOCATE EY(1),EX(1):PRINT"*";
540 LOCATE EY(2),EX(2):PRINT"*";
550 LOCATE Y1,X1:COLOR 23,0:PRINT"O";:COLOR 7
1000 A$=INKEY$:IF A$=""THEN 1500
1002 IF LEN(A$)=2 THEN R$=RIGHT$(A$,1) ELSE R$=""
1005 PX=X1:PY=Y1:XX=0:YY=0
1010 IF R$="M"THEN XX=1:X1=X1+XX:IF X1>39 THEN X1=39
1020 IF R$="K"THEN XX=-1:X1=X1+XX:IF X1<2 THEN X1=2
1030 IF R$="H"THEN YY=-1:Y1=Y1+YY:IF Y1<2 THEN Y1=2
1040 IF R$="P"THEN YY=1:Y1=Y1+YY:IF Y1>22 THEN Y1=22
1050 SC=SCREEN(Y1,X1)
1060 IF SC=219 THEN GOSUB 2000
1065 IF SC=42 THEN 3000
1070 LOCATE PY,PX:PRINT" ";:LOCATE Y1,X1:PRINT"O";
1499 ' Move enemies
1500 D=D-1:IF D>0 THEN 1000 ELSE D=DIF
1501 IF E1=1 THEN E1=2 ELSE E1=1
1502 PEX=EX(E1):PEY=EY(E1):IF ED(E1) THEN 1780
1503 EXX=SGN(X1-EX(E1)):EYY=SGN(Y1-EY(E1))
1505 EX(E1)=EX(E1)+EXX:EY(E1)=EY(E1)+EYY
1510 ESC=SCREEN(EY(E1),EX(E1))
1520 IF ESC=219 OR ESC=177 OR ESC=42 THEN EY(E1)=PEY:EX(E1)=PEX:GOTO 1700
1530 IF ESC=79 THEN 3000
1600 LOCATE PEY,PEX:PRINT" ";
1610 LOCATE EY(E1),EX(E1):PRINT"*";:GOTO 1000
1699 ' Stuck
1700 FOR I=1 TO 10:EXX=INT(RND*3)-1:EYY=INT(RND*3)-1
1710 ESC=SCREEN(EY(E1)+EYY,EX(E1)+EXX)
1720 IF ESC=32 THEN EY(E1)=EY(E1)+EYY:EX(E1)=EX(E1)+EXX:ED(E1)=0:I=20:GOTO 1770
1770 NEXT:IF I>15 THEN 1600
1780 FOR EXX=-1 TO 1:FOR EYY=-1 TO 1:IF EYY=0 AND EXX=0 THEN 1800
1790 SC=SCREEN(EY(E1)+EYY,EX(E1)+EXX):IF SC=32 THEN EXX2=EXX:EXX=100:EYY2=EYY:EYY=100
1800 NEXT:NEXT:IF EXX>90 THEN EX(E1)=EX(E1)+EXX2:EY(E1)=EY(E1)+EYY2:ED(E1)=0:GOTO 1600
1810 ED(E1)=-1:IF ED(1) AND ED(2) THEN 3100
1820 GOTO 1000
1999 ' Block pusher
2000 XP=X1:YP=Y1
2010 XP=XP+XX:YP=YP+YY:SP=SCREEN(YP,XP)
2020 IF SP=177 OR SP=42 THEN X1=PX:Y1=PY:RETURN 1500
2030 IF SP<>32 THEN 2010
2040 LOCATE YP,XP:COLOR 2:PRINT"Û";:COLOR 7:RETURN
2999 'YOU LOSE
3000 CLS:PRINT"YOU'RE DEAD"
3005 FOR I=1 TO 100:A$=INKEY$:NEXT
3010 GOTO 3107
3100 CLS:PRINT"You got 'em
3105 FOR I=1 TO 100:A$=INKEY$:NEXT
3107 PRINT"Press any key to play again"
3110 A$=INPUT$(1):GOTO 10
