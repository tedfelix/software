0 CLS
1 ' ZAP COPYRIGHT 1978 TOM MARAZITA
2 ' CURSOR #2 AUGUST 1978
3 ' P.O. BOX 550, GOLETA, CA 93017
4 '
10 ' AS OF JULY 4, 1979 RJ
11 DEF FNCOL(Z)=(Z-32727)MOD 40
12 DEF FNROW(Z)=(Z-32727)\40
13 KEY OFF
14 DEFINT A-Z
15 RANDOMIZE VAL(RIGHT$(TIME$,2))
96 DIM WM(255),WN(255)
99 EL=18:HP=EL-1
100 CLS:LOCATE CSRLIN+4:PRINT"CURSOR #2 - ZAP"
102 LOCATE CSRLIN+1:PRINT"CHASE THE TARGETS!
105 DIM A(255),B(255)
110 GOSUB 60000
120 CLS:PRINT
130 RO=23:CO=38
140 S=1
160 LOCATE 1,1:PRINT CHR$(218);:FOR X=2 TO 38:LOCATE,X:PRINT CHR$(196);:NEXT
170 LOCATE 1,39:PRINT CHR$(191);:FOR X=2 TO 23:LOCATE X,39:PRINT CHR$(179);:NEXT
180 LOCATE 24,39:PRINT CHR$(217);:FOR X=38 TO 2 STEP-1:LOCATE 24,X:PRINT CHR$(196);:NEXT
190 LOCATE 24,1:PRINT CHR$(192);:FOR X=23 TO 2 STEP-1:LOCATE X,1:PRINT CHR$(179);:NEXT
195 GOSUB 900
200 A$=INKEY$:IF A$<>""THEN 200
211 GOTO 410
220 A$="8":GOTO 240
230 A$=INKEY$:IF LEN(A$)=2 THEN R$=RIGHT$(A$,1)
240 IF A$="4"OR R$="K" THEN DC=-1:DR=0:A$="4"
250 IF A$="8"OR R$="H" THEN DC=0:DR=-1:A$="8"
260 IF A$="6"OR R$="M" THEN DC=1:DR=0:A$="6"
270 IF A$="2"OR R$="P" THEN DC=0:DR=1:A$="2"
280 IF A$="7"OR R$="G" THEN DC=-1:DR=-1:A$="7"
290 IF A$="9"OR R$="I" THEN DC=1:DR=-1:A$="9"
300 IF A$="3"OR R$="Q" THEN DC=1:DR=1:A$="3"
310 IF A$="1"OR R$="O" THEN DC=-1:DR=1:A$="1"
311 IF A$="5"THEN 770
320 CO=CO+DC:RO=RO+DR
321 IF RO=1 OR RO=24 OR CO=1 OR CO=39 THEN 590
324 IF SCREEN(RO,CO)=178 THEN GOSUB 550
325 WM(HP)=RO:WN(HP)=CO:HP=HP+1
326 IF HP>EL THEN HP=0
350 IF SCREEN(RO,CO)=2 THEN 590
360 LOCATE RO,CO:PRINT CHR$(2)
370 R=INT(RND(1)*15)+1:IF S>5 THEN ZZ=20
375 P1=INT(RND(1)*ZZ)+1:ZZ=50
380 IF R=3 THEN 410
390 IF P1=3 THEN 480
400 GOSUB 730:GOTO 230
410 '
420 IF S>6 THEN 400
430 RQ=INT(RND(1)*20)+2
435 CQ=INT(RND(1)*36)+2
440 IF SCREEN(RQ,CQ)<>32 THEN 430
450 S=S+1:H=H+1:TC=TC+1
460 LOCATE RQ,CQ:PRINT CHR$(178);
470 A(H)=RQ:B(H)=CQ:IF CV=1 THEN 400
475 CV=1:GOTO 220
480 '
490 P=INT(RND(1)*H)+1
500 '
510 IF SCREEN(A(P),B(P))<>178 THEN 540
520 LOCATE A(P),B(P):PRINT" ";
530 S=S-1
540 A(P)=A(H):B(P)=B(H):H=H-1:GOTO 400
550 '
560 SC=SC+10
565 S=S-1
566 EL=EL+2
567 IF EL>250 THEN EL=250
570 GOSUB 900
580 RETURN
590 CR=CR+1
595 GOSUB 900
600 IF CS>L THEN L=CS
610 CS=0
615 EL=18:HP=17
616 TP=0
620 CV=0
635 FOR T=1 TO 8:FOR X=1 TO 100:NEXT:LOCATE RO,CO:PRINT CHR$(1);:FOR X1=1 TO 100:NEXT:LOCATE RO,CO:PRINT CHR$(2);:NEXT
636 FOR X=0 TO 255:WM(X)=0:WN(X)=0:NEXT:HP=17:TP=0
638 IF CR=6 THEN 650
640 R$="":GOTO 120
650 CLS:LOCATE CSRLIN+6:PRINT
660 PRINT"YOUR  SCORE IS"SC
662 PRINT
666 PRINT
667 PRINT"HIGHEST POSSIBLE SCORE:"TC*10
670 PRINT
675 PRINT"YOU HIT";INT((SC/(TC*10))*100);"% OF THE TARGETS"
677 Q$=INKEY$:IF Q$<>""THEN 677:' WAS POKE525,0
680 LOCATE CSRLIN+1:PRINT"WANT TO PLAY AGAIN?"
681 RJ$=INKEY$:IF RJ$=""THEN 681
682 IF RJ$="Y"THEN RUN
685 LOCATE CSRLIN+1:PRINT"HOPE YOU HAD FUN!";:LOCATE CSRLIN+1:PRINT
700 END
730 R1=WM(TP):C1=WN(TP):TP=TP+1
740 IF TP>EL THEN TP=0
745 IF R1=0 OR C1=0 THEN 760
750 LOCATE R1,C1:PRINT" ";
760 RETURN
770 FOR E=1 TO 171:A$=INKEY$:IF A$<>""THEN 240
780 NEXT E:SC=SC-10:IF SC<0 THEN SC=0
790 GOSUB 900
800 GOTO 770
900 LOCATE 1,1:LOCATE,POS(0)+3:PRINT"SCORE="SC;;:LOCATE,19:PRINT;"NO. OF CRASHES="CR
910 RETURN
2000 PRINT"PRESS A SET OF KEYS AND I'LL TELL YOU THE ASCII VALUE(S)"
2002 A$=INKEY$:IF A$=""THEN 2002
2003 PRINT"A$=";A$;"  LEN(A$)=";LEN(A$);:PRINT"ASC(A$)=";
2005 FOR I=1 TO LEN(A$)
2010 PRINT ASC(MID$(A$,I,1));
2015 NEXT
2017 PRINT
2020 GOTO 2000
60000 LOCATE CSRLIN+1:PRINT"DO YOU NEED INSTRUCTIONS ?"
60002 RJ$=INKEY$:IF RJ$=""THEN 60002
60004 IF RJ$="Y" OR RJ$="y"THEN 60030
60020 RETURN
60030 CLS:LOCATE CSRLIN+2:PRINT"YOU CONTROL A STRANGE MISSILE!"
60040 LOCATE CSRLIN+1:PRINT"TARGETS '&' APPEAR ON THE SCREEN, WHICH"
60050 PRINT"YOU TRY TO HIT.  USE NUMBERS ON THE NUMERIC KEYPAD AT"
60060 PRINT"THE RIGHT OF THE KEYBOARD TO CONTROL THE MISSILE: "
60070 PRINT"             8 MOVES YOU UP.
60080 PRINT"             2 MOVES YOU DOWN.
60090 PRINT"             4 MOVES YOU LEFT
60100 PRINT"             6 MOVES YOU RIGHT.
60102 PRINT
60105 PRINT"(1, 3, 7, AND 9 MOVE YOU DIAGONALLY)
60110 PRINT
60112 PRINT"EACH TARGET IS WORTH 10 POINTS, BUT
60125 PRINT"YOUR MISSLE GETS BIGGER AFTER HITTING A
60128 PRINT"TARGET...
60130 LOCATE CSRLIN+1:PRINT"THE GAME ENDS AFTER 6 CRASHES!
60300 LOCATE CSRLIN+2:PRINT"PRESS 'SPACE' TO BEGIN THE GAME
60310 ZZ$=INKEY$:IF ZZ$=""THEN GOTO 60310
60320 RETURN

