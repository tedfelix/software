1 DEF FNARRAYFIG(CARD)=(INT((CARD-1)/100)-1)*13+((CARD-1)-INT((CARD-1)/100)*100)
2 DEF FNRAND(X)=INT(RND*X)+1
3 DEF FNRANK(CARD)=CARD-INT(CARD/100)*100
4 DEF FNSUIT(CARD)=INT(CARD/100)
5 ' Copyright 2017, Ted Felix
6 ' File date: 7/26/1984 02:10:44 PM
8 RANDOMIZE VAL(TIME$)+VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
9 OPTION BASE 1
10 DIM CARD(52),USED(52),TABLE(52)
15 SP$=SPACE$(39)
17 SCREEN 0:WIDTH 40:KEY OFF:CLS
18 PRINT"Draw Poker":PRINT"For The IBM Personal Computer"
20 ' Set-up deck
30 C=0:FOR I=100 TO 400 STEP 100
40 FOR X=2 TO 14
50 C=C+1:CARD(C)=I+X
60 NEXT:NEXT
70 RANK$(1)="J":RANK$(2)="Q":RANK$(3)="K":RANK$(4)="A"
75 RESTORE 8000
80 GOSUB 8200:HAND$(W)=HAND$:IF HAND$="Nothing" THEN 100
90 NU#(W)=N#:GOTO 80
100 '
999 '  Usual start
1000 GOSUB 10900
1010 GOSUB 9300:CLS
1020 ' Computer or player deal
1030 GOSUB 10700
1040 IF DEALER=0 THEN 1500 ' Computer
1050 ' Human
1055 GOSUB 9100
1060 BE=1:GOSUB 2000 COMP BET OR FOLD
1065 IF FOLD=1 THEN 2300
1069 GOSUB 11200
1070 GOSUB 10400 HUMAN RAISE CALL...
1075 IF FOLD=1 THEN 2450
1077 IF RAISE=1 THEN BE=2:GOSUB 2000:GOTO 1065
1080 GOSUB 4000 COMP DISCARD
1085 GOSUB 11200
1090 GOSUB 10100 HUMAN DISCARD
1100 BE=1:GOSUB 2000 COMP BET OR FOLD
1110 GOSUB 10400
1112 IF FOLD=1 THEN 2450
1115 IF RAISE THEN BE=2:GOSUB 2000:GOTO 1192
1120 GOTO 8900
1192 GOSUB 11200
1195 IF FOLD=1 THEN 2300
1200 GOTO 8900
1499 END
1500 GOSUB 9100:GOSUB 10300
1505 IF FOLD=1 THEN 2450
1507 IF CAL=1 THEN 1520
1510 BE=2:GOSUB 2000
1512 GOSUB 11200
1515 IF RAISE=1 THEN GOSUB 10400:GOTO 1505
1520 GOSUB 10100
1530 GOSUB 4000
1535 GOSUB 11200
1540 GOSUB 10300
1545 IF FOLD=1 THEN 2450
1547 IF CAL=1 THEN 1570
1550 BE=2:GOSUB 2000
1560 IF RAISE=1 THEN GOSUB 10400:GOTO 1545
1570 GOTO 8900
1999 END ' Computer Call, Raise, Fold...
2000 IF BE=2 THEN 2500
2010 H=0:GOSUB 8500
2020 IF W=10 AND FNRANK(CHAND(5))>9 THEN 2100
2030 IF W=10 AND FNRANK(CHAND(5))<10 THEN 2200
2100 GOSUB 10200:LOCATE 15,1
2110 PRINT"I'll bet $";:A=(9-W)*10+(FNRANK(CHAND(5)))*10+FNRAND(100)
2120 PRINT A:CBET=CBET+A:GOSUB 10800
2130 FOR I=0 TO 2000:NEXT:RETURN
2200 PRINT"I fold":HWIN=HWIN+CBET:GOSUB 9500
2300 HWIN=HWIN+CBET:CWIN=CWIN-CBET:GOSUB 10200:LOCATE 15,1
2310 PRINT"You win!":GOSUB 9500:GOSUB 11100
2410 FOR I=0 TO 3000:NEXT:GOTO 10750
2450 HWIN=HWIN-HBET:CWIN=CWIN+HBET
2460 GOSUB 10200:GOSUB 9500:LOCATE 15,1:PRINT"I win!":GOSUB 11100
2470 FOR I=0 TO 3000:NEXT:GOTO 10750
2500 ' Computer Call, Raise and Fold
2510 LOCATE 15,1:H=0:GOSUB 8500
2520 IF W<4 THEN PRINT"I'll see that bet and raise you";:RAISE=1:CAL=0:A=FNRAND(100):PRINT A:CBET=CBET+A:GOSUB 10800:RETURN
2530 IF W>5 AND HBET<=900 THEN PRINT"I'll call":CAL=1:RAISE=0:RETURN
2540 IF HBET>900 AND FNRAND(10)=1 THEN FOLD=1:CAL=0:RAISE=0
2550 RETURN
3999 END ' Computer Discard
4000 GOSUB 8500:IF HND<7 THEN PRINT"I'll pass":RETURN
4010 IF HND=7 THEN 4100
4020 IF HND=9 THEN 4200
4030 IF HND=8 THEN 4300
4040 Z=1:DISCARD=0:FOR I=1 TO 5
4050 IF FNRANK(CHAND(I))<10 THEN DISCARD(Z)=I:DISCARD=DISCARD+1:Z=Z+1
4060 NEXT:PRINT"I'll discard";DISCARD:GOTO 4900
4100 DISCARD=2
4110 PRINT"I'll discard";DISCARD
4120 Z=1:FOR I=1 TO 5:IF FNRANK(CHAND(I))=TEMP(1) THEN DISCARD(Z)=I:Z=Z+1
4130 NEXT:IF Z<>DISCARD THEN PRINT"Error":STOP ELSE GOTO 4900
4200 DISCARD=3:GOTO 4110
4300 FOR I=1 TO 5:X=FNRANK(CHAND(I))
4310 IF X<>TEMP(1) AND X<>TEMP(2) THEN DISCARD(1)=I
4320 NEXT:DISCARD=1:PRINT"I'll discard 1"
4900 GOSUB 9900:RETURN
5999 END ' Begining of Workarea
6000 H=1:RESTORE 8000:FOR I=1 TO 5:PRINT"Card #";I;:INPUT HHAND(I):NEXT
6010 GOSUB 8500
6020 PRINT HAND$(W):PRINT"Hand rank";W;"Total worth";WORTH
6030 GOTO 6000
7980 END ' End of Workarea
7990 ' The following is a list of
7991 ' all possible draw poker hands
7992 ' (with no wild cards).  The
7993 ' list is in rank order and
7994 ' is in the simplest form for
7995 ' the computer to handle.
8000 DATA Royal Flush,X,10,11,12,13,14,X,4,1
8010 DATA Straight Flush,X,1,2,3,4,5,*,40,2
8020 DATA Four of a kind,*,1,1,1,1,0,Z,624,3
8030 DATA Four of a kind,*,0,1,1,1,1,Z,624,3
8040 DATA Full House,*,1,1,2,2,2,Z,3744,4
8050 DATA Full House,*,1,1,1,2,2,Z,3744,4
8060 DATA Flush,X,0,0,0,0,0,Z,5108,5
8070 DATA Straight,*,1,2,3,4,5,*,10200,6
8080 DATA Three of a kind,*,1,1,1,0,0,Z,54912,7
8090 DATA Three of a kind,*,0,1,1,1,0,Z,54912,7
8100 DATA Three of a kind,*,0,0,1,1,1,Z,54912,7
8110 DATA Two pair,*,1,1,2,2,0,Z,123552,8
8120 DATA Two pair,*,0,1,1,2,2,Z,123552,8
8125 DATA Two pair,*,1,1,0,2,2,Z,123552,8
8130 DATA Pair,*,1,1,0,0,0,Z,1098240,9
8140 DATA Pair,*,0,1,1,0,0,Z,1098240,9
8150 DATA Pair,*,0,0,1,1,0,Z,1098240,9
8160 DATA Pair,*,0,0,0,1,1,Z,1098240,9
8170 DATA Nothing,*,0,0,0,0,0,Z,1302540,10
8199 ' Get one hand
8200 READ HAND$,SU$:FOR I=1 TO 5:READ C(I):NEXT:READ DE$,N#,W:RETURN
8249 ' Same Suit?
8250 FOR I=1 TO 5:H(I)=FNSUIT(HAND(I)):NEXT
8260 IF H(1)=H(2) AND H(2)=H(3) AND H(3)=H(4) AND H(4)=H(5) THEN YES=1 ELSE YES=0
8270 RETURN
8299 ' Correct hand rank?
8300 GOSUB 8700
8320 IF DE$="X" THEN 8350
8322 IF DE$="*" THEN 8400
8324 IF DE$="Z" THEN 8450
8330 PRINT"Error in data"
8340 END:GOTO 8330 ' Don't try a `CONT'
8350 YES=1:FOR I=1 TO 5:IF C(I)<>FNRANK(HAND(I)) THEN YES=0:I=7
8360 NEXT:RETURN
8400 YES=1:FOR I=2 TO 5:IF FNRANK(HAND(I-1))<>FNRANK(HAND(I)-1) THEN YES=0:I=7
8410 NEXT:RETURN
8450 FOR I=1 TO 5:TEMP(I)=0:NEXT:YES=1:FOR I=1 TO 5
8460 IF C(I)=0 THEN 8480 ELSE C=C(I)
8470 IF TEMP(C)=0 THEN TEMP(C)=FNRANK(HAND(I)) ELSE IF FNRANK(HAND(I))<>TEMP(C) THEN YES=0:I=7
8480 NEXT
8490 RETURN
8499 ' What Hand is this?
8500 RESTORE 8000:FOR I=1 TO 10:HND(I)=0:NEXT
8505 GOSUB 8700
8520 GOSUB 8200
8530 IF SU$="X" THEN GOSUB 8250:IF YES=0 THEN 8570
8540 GOSUB 8300:IF YES=0 THEN 8570
8550 HND=W:GOTO 8580
8570 GOTO 8520
8580 GOSUB 8600
8585 RETURN
8599 ' What is it worth?
8600 WORTH=0:ON W GOTO 8650,8650,8610,8650,8650,8650,8620,8630,8640,8650
8610 WORTH=TEMP(1)*4:RETURN
8620 WORTH=TEMP(1)*3:RETURN
8630 WORTH=TEMP(1)*2+TEMP(2)*2:RETURN
8640 WORTH=TEMP(1)*2:RETURN
8650 WORTH=0:FOR I=1 TO 5
8655 WORTH=WORTH+FNRANK(HAND(I))
8660 NEXT:RETURN
8700 IF H=1 THEN 8730
8710 FOR I=1 TO 5:HAND(I)=CHAND(I):NEXT
8720 RETURN
8730 FOR I=1 TO 5:HAND(I)=HHAND(I):NEXT
8740 RETURN
8900 H=0:GOSUB 8500:CHND=HND:CWOR=WORTH
8910 H=1:GOSUB 8500:HHND=HND:HWOR=WORTH
8920 IF HHND<CHND THEN 2300
8930 IF CHND<HHND THEN 2450
8940 PRINT"It's close!":FOR I=0 TO 3000:NEXT
8950 IF HWOR>CWOR THEN 2300
8960 IF CWOR>HWOR THEN 2450
8970 PRINT"Push! Neither of us wins a cent!"
8980 GOTO 10750
8998 END
8999 ' Card Symbol and Color Translator
9000 SUIT=INT(CARD/100):SUIT$=CHR$(4-SUIT+3)
9010 RANK=CARD-SUIT*100
9020 IF RANK>10 THEN RANK$=RANK$(RANK-10)
9030 IF RANK<11 AND RANK>1 THEN RANK$=STR$(RANK):RANK$=RIGHT$(RANK$,LEN(RANK$)-1)
9050 IF SUIT=1 OR SUIT=2 THEN COLR=0 ELSE COLR=4
9060 IF RANK=10 THEN RANK$="0"
9090 RETURN
9099 ' Display Human's Hand
9100 LOCATE 1
9110 D$="Û":T$=STRING$(4,D$)
9120 S$=SPACE$(2)
9130 A$=T$+S$+T$+S$+T$+S$+T$+S$+T$
9140 PRINT A$:PRINT A$
9150 FOR I=1 TO 5
9160 CARD=HHAND(I):GOSUB 9000
9170 PRINT D$;:COLOR 0,7:PRINT RANK$;
9180 COLOR COLR,7:PRINT SUIT$;:COLOR 7,0
9190 PRINT D$;"  ";
9200 NEXT:PRINT:PRINT A$:PRINT A$
9205 A$="     ":PRINT"1";A$;"2";A$;"3";A$;"4";A$;"5"
9210 LOCATE 8:T$=STRING$(4,15)
9220 S$="  ":A$=T$+S$+T$+S$+T$+S$+T$+S$+T$
9230 FOR I=1 TO 5:PRINT A$:NEXT
9240 GOSUB 10800:RETURN
9299 ' Deal
9300 FOR I=1 TO 52:TABLE(I)=0:NEXT:FOR I=1 TO 5
9310 GOSUB 10000
9320 HHAND(I)=DEAL
9330 GOSUB 10000
9340 CHAND(I)=DEAL
9350 NEXT:GOSUB 9700:RETURN
9400 DELT=0:FOR Q=1 TO 52:IF TABLE(Q)=0 THEN USED(Q)=0
9420 NEXT
9470 RETURN
9500 LOCATE 8
9510 D$="Û":T$=STRING$(4,D$)
9520 S$=SPACE$(2)
9530 A$=T$+S$+T$+S$+T$+S$+T$+S$+T$
9540 PRINT A$:PRINT A$
9550 FOR I=1 TO 5
9560 CARD=CHAND(I):GOSUB 9000
9570 PRINT D$;:COLOR 0,7:PRINT RANK$;
9580 COLOR COLR,7:PRINT SUIT$;:COLOR 7,0
9590 PRINT D$;"  ";
9600 NEXT:PRINT:PRINT A$:PRINT A$
9610 H=0:GOSUB 8500:LOCATE 13,1:PRINT HAND$(HND)
9690 GOSUB 10200:RETURN
9699 ' Sort Human and Computer Hands
9700 FOR T=1 TO 5
9710 FOR I=1 TO 4
9720 X=FNRANK(HHAND(I))
9730 Y=FNRANK(HHAND(I+1))
9740 IF X>Y THEN SWAP HHAND(I),HHAND(I+1)
9750 X=FNRANK(CHAND(I))
9760 Y=FNRANK(CHAND(I+1))
9770 IF X>Y THEN SWAP CHAND(I),CHAND(I+1)
9780 NEXT:NEXT:RETURN
9799 ' Human Discard
9800 IF DISCARD=0 THEN RETURN
9805 FOR I=1 TO DISCARD:H=HHAND(DISCARD(I))
9810 AL=FNARRAYFIG(H)
9820 TABLE(AL)=0:GOSUB 10000
9830 HHAND(DISCARD(I))=DEAL:NEXT
9840 GOSUB 10600:GOSUB 9700:RETURN
9899 ' Computer Discard
9900 FOR I=1 TO DISCARD:IF DISCARD(I)>0 THEN H=CHAND(DISCARD(I))
9905 PRINT"discarding";H;"Card no.";DISCARD(I)
9910 AL=FNARRAYFIG(H):IF TABLE(AL)<>1 THEN PRINT"oopsie!":END
9920 TABLE(AL)=0:GOSUB 10000
9930 CHAND(DISCARD(I))=DEAL:NEXT
9940 GOSUB 10600:GOSUB 9700:RETURN
9999 ' Deal one card
10000 C=FNRAND(52):IF USED(C)=1 THEN 10000
10010 TABLE(C)=1:DELT=DELT+1:USED(C)=1
10020 DEAL=CARD(C)
10030 IF DELT>=52 THEN GOSUB 9400
10040 RETURN
10099 ' Ask Human for Discards
10100 GOSUB 10200
10105 LOCATE 15,1:PRINT"Which cards would you like to throw     away?"
10110 FOR I=1 TO 4:DISCARD(I)=0:NEXT
10115 DISCARD=-1:FOR I=1 TO 5
10120 A$=INKEY$:IF A$="" THEN 10120
10130 IF A$="A" OR A$="a" THEN LOCATE 17,1:PRINT SP$:GOTO 10105
10140 IF A$=CHR$(13) THEN DISCARD=I-1:I=6 :GOTO 10170
10150 IF A$<"1" OR A$>"5" THEN 10120
10160 DISCARD(I)=VAL(A$)
10165 FLAG=0:FOR G=1 TO I-1:IF G<>I THEN IF DISCARD(G)=DISCARD(I) THEN G=100:FLAG=1
10167 NEXT:IF FLAG=1 THEN 10120
10170 PRINT A$;:NEXT
10180 IF DISCARD=-1 THEN PRINT:PRINT:PRINT"No More Than 4 Discards":LOCATE 17,1:PRINT SP$:GOTO 10105
10190 IF DISCARD=0 THEN RETURN ELSE GOSUB 9800
10195 GOSUB 10200:GOSUB 9100:RETURN
10199 ' Clear message area
10200 LOCATE 15,1
10210 FOR I=1 TO 7:PRINT SP$:NEXT
10220 RETURN
10299 ' Human bet or fold
10300 GOSUB 10200
10305 LOCATE 15,1
10310 IF HWIN=0 THEN PRINT"You're even"
10320 IF HWIN>0 THEN PRINT"You're ahead ";USING "$####.##";HWIN
10330 IF HWIN<0 THEN PRINT"You're behind ";USING "$####.##";ABS(HWIN)
10340 INPUT "What's you're bet";HBET$:GOSUB 10370:IF HBET$="FOLD" THEN RETURN 2450
10350 H=+VAL(HBET$):IF HBET>1000 THEN PRINT:PRINT"House limit is $1000":LOCATE 16,1:PRINT SP$:GOTO 10305
10355 IF FOLD<>1 AND H<1 THEN PRINT:PRINT"Be Realistic        ":LOCATE 16,1:PRINT SP$:GOTO 10305
10360 HBET=HBET+H:GOSUB 10200:GOSUB 10800:RETURN
10370 FOR I=1 TO LEN(HBET$):T$=MID$(HBET$,I,1)
10380 IF T$>="a" AND T$<="z" THEN MID$(HBET$,I,1)=CHR$(ASC(T$)-32)
10390 NEXT:RETURN
10399 ' Human Raise, Call or Fold
10400 GOSUB 10200:LOCATE 15,1
10410 PRINT"Raise, Call or Fold?"
10420 A$=INKEY$:IF A$="" THEN 10420
10430 IF A$="R" OR A$="r" THEN RAISE=1:CAL=0:GOTO 10500
10440 IF A$="C" OR A$="c" THEN RAISE=0:CAL=1:HBET=CBET:RETURN
10450 IF A$="F" OR A$="f" THEN FOLD=1:RETURN
10460 GOTO 10420
10499 ' Human Raise
10500 GOSUB 10200:RAISE=1
10510 LOCATE 15,1:PRINT"I bet";CBET:PRINT"You've bet";HBET:INPUT"Raise";R
10520 IF R<0 THEN PRINT:PRINT"Be Realistic":LOCATE 16,1:PRINT SP$:GOTO 10510
10530 CHECK=HBET+R:IF CHECK>1000 THEN PRINT"House limit is $1000":LOCATE 16,1:PRINT SP$:GOTO 10510
10540 HBET=CHECK:GOSUB 10200:RETURN
10599 ' Internal "TABLE" Array Error
10600 ERO=0:FOR I=1 TO 52
10610 IF TABLE(I)=1 THEN ERO=ERO+1
10620 NEXT:IF ERO<>10 THEN 10640
10630 RETURN
10640 PRINT"Table error, contact Ted":END
10650 ERROR (17):GOTO 10650
10699 ' Ante
10700 HBET=10:CBET=10:RETURN
10749 ' Play again?
10750 GOSUB 10200:LOCATE 15,1
10760 PRINT"Another hand?"
10770 A$=INKEY$:IF A$="" THEN 10770
10780 IF A$="Y" OR A$="y" THEN IF DEALER=0 THEN DEALER=1:GOTO 1010 ELSE DEALER=0:GOTO 1010
10790 IF A$="N" OR A$="n" THEN CLS:PRINT"Hope you enjoyed playing Draw Poker":END
10795 GOTO 10770
10799 ' Print Pot
10800 LOCATE 2,30:PRINT"Pot:";
10810 LOCATE 3,30:PRINT HBET+CBET
10820 LOCATE 15,1:RETURN
10900 PRINT"I'll have my friend ROM pick a couple   of numbers between 1 and 100 and the    highest deals."
10910 INPUT"Would you like the first or the second  number (1 or 2)";WHICH
10920 A=FNRAND(100):B=FNRAND(100)
10925 IF A=B THEN 10920
10930 PRINT"The first number is";A
10940 FOR I=1 TO 1000:NEXT
10950 PRINT"The second number is";B
10960 IF WHICH=1 THEN IF A>B THEN 11000 ELSE 11010
10970 IF B>A THEN 11000 ELSE 11010
11000 PRINT"You deal!":DEALER=1:GOTO 11020
11010 PRINT"I deal!":DEALER=0
11020 FOR I=0 TO 3500:NEXT:CLS:RETURN
11100 LOCATE 16,1:PRINT"You're ";
11110 IF HWIN=0 THEN PRINT"even"
11120 IF HWIN>0 THEN PRINT"ahead ";USING "$####.##";HWIN
11130 IF HWIN<0 THEN PRINT"behind ";USING "$####.##";ABS(HWIN)
11140 PRINT"and I'm ";
11150 IF CWIN=0 THEN PRINT"even."
11160 IF CWIN>0 THEN PRINT"ahead ";USING "$####.##";CWIN
11170 IF CWIN<0 THEN PRINT"behind ";USING "$####.##";ABS(CWIN)
11180 RETURN
11200 FOR I=0 TO 3000:NEXT:RETURN

