1 ' 8/3/83   - Original version
2 ' 10/26/91 - Turned off cursor
3 ' Copyright 2017, Ted Felix
10 RANDOMIZE TIMER
15 DIM A(50)
20 DEF FNRAND(X)=INT(RND*X)+1
30 CLS:SCREEN 0,0,0:WIDTH 40:KEY OFF:LOCATE ,,0
40 PRINT"SIMON":WR=0:N1=0:N2=0
190 N2=N2+1:N1=N1+1:LOCATE 25,1:PRINT"Number:"N1;TAB(20)"Wrong:"WR;
200 A(N1)=FNRAND(4)
220 FOR I=1 TO N1:LOCATE 12,20:PRINT A(I)
230 SOUND A(I)*50+50,4:FOR DE=1 TO 500:NEXT:LOCATE 12,20:PRINT"  ";:NEXT
240 FOR I=1 TO 15:A$=INKEY$:NEXT
250 FOR I=1 TO N1:TI=0
260 A$=INKEY$:IF A$=""AND TI<100 THEN TI=TI+1:GOTO 260 ELSE IF TI>=100 THEN 290
270 IF A$<>RIGHT$(STR$(A(I)),1) THEN 290
280 LOCATE 12,21:PRINT A$:SOUND A(I)*50+50,4:FOR DE=1 TO 300:NEXT:LOCATE 12,21:PRINT" ";:ERRO=0:GOTO 300
290 I=N1:ERRO=1
300 NEXT
310 IF ERRO=1 THEN 400 ELSE FOR DE=1 TO 1000:NEXT:GOTO 190
400 WR=WR+1:SOUND 40,20
410 LOCATE 25,20:PRINT"Wrong:"WR;:FOR DE=1 TO 2000:NEXT
420 IF WR>=10 THEN 1000
430 GOTO 220
1000 CLS
1010 PRINT"You got 10 wrong"
1020 PRINT"You made it to";N1
1030 IF N1<5 THEN PRINT"You need a lot of practice!!":GOTO 1090
1040 IF N1<10 THEN PRINT"Not bad":GOTO 1090
1050 IF N1<20 THEN PRINT"Very Good!":GOTO 1090
1060 IF N1<30 THEN PRINT"An Excellent Job!":GOTO 1090
1070 PRINT"Wow!!!! You must be a computer!"
1090 PRINT:PRINT"Play again [Y/N]"
1100 A$=INPUT$(1)
1110 IF A$="y"OR A$="Y"THEN 30
1120 END

