5 ' ALIEN INVASION
6 ' By: Ted Felix
8 DEFINT A-Z
10 DEF FNRAND(X)=INT(RND*X)+1
20 CX=20:CY=22:EX=FNRAND(40):EY=1
200 WIDTH 40:KEY OFF:CLS:PRINT"Alien Invasion
210 PRINT"By: Ted Felix
220 PRINT:PRINT"Controls:
230 PRINT"   "CHR$(24)" Up
235 PRINT"   "CHR$(25)" Down
240 PRINT"   "CHR$(26)" Right
245 PRINT"   "CHR$(27)" Left
250 PRINT"   <Space Bar> Fire
255 PRINT:PRINT"Scoring:
260 PRINT"   $  0 Points
265 PRINT"   * 10 Points
270 COLOR 4,0:PRINT"   #";:COLOR 7,0:PRINT" 50 Points"
390 LOCATE 24,1:PRINT"Press any key to play";
400 A$=INPUT$(1)
1000 LOCATE 1,1,0:CLS:EL=5:S1=0
1010 X=FNRAND(40):Y=FNRAND(12):T=FNRAND(10)
1020 IF T>0 AND T<10 THEN LOCATE Y,X:IF FNRAND(2)=1 THEN PRINT"$"ELSE PRINT" ";
1025 IF T=10 THEN LOCATE Y,X:PRINT"*";
1030 IF FNRAND(100)=1 AND FE=0 THEN FE=1
1040 PY=CY:PX=CX:A$=INKEY$
1045 IF LEN(A$)=2 THEN R$=RIGHT$(A$,1) ELSE R$=""
1050 IF R$="K"THEN CX=CX-1:IF CX<1 THEN CX=40
1060 IF R$="M"THEN CX=CX+1:IF CX>40 THEN CX=1
1070 IF A$=" "THEN GOSUB 2000
1080 IF R$="H"THEN CY=CY-1:IF CY<1 THEN CY=1
1090 IF R$="P"THEN CY=CY+1:IF CY>23 THEN CY=23
1350 IF SCREEN(CY,CX)<>32 THEN CY=PY:CX=PX
1370 LOCATE 24,1:PRINT SC;TAB(10)EL;
1380 LOCATE PY,PX:PRINT" ";
1390 LOCATE CY,CX:PRINT"^";
1400 IF FE=1 THEN GOSUB 3000
1410 IF SC-S1>=1000 THEN GOSUB 5000:CLS:S1=SC
1420 GOTO 1010
1999 ' Fire!
2000 COLOR 4,0:FOR FY=CY-2 TO 1 STEP-1
2010 A=SCREEN(FY,CX):IF A=32 THEN 2100
2020 IF A=42 THEN SC=SC+10:F=FY:FY=-1:GOTO 2120
2030 IF A=36 THEN F=FY:FY=-1:GOTO 2120
2040 IF A=35 THEN SC=SC+50:F=FY:FY=-1:GOSUB 4000:FE=0:GOTO 2120
2050 F1=0:IF A=219 THEN F1=1:F=FY:FY=-1:GOTO 2120
2060 F2=0:IF A=79 OR A=179 THEN F2=1:F=FY:FY=-1:GOTO 2120
2100 LOCATE FY,CX:PRINT"Г";
2110 LOCATE FY+1,CX:PRINT" ";:F=FY
2120 NEXT:LOCATE F,CX:PRINT" ";
2130 LOCATE F+1,CX:PRINT" ";
2140 COLOR 7,0:RETURN
2999 ' Enemy Craft
3000 DX=DX+FNRAND(3)-2
3010 IF DX>1 THEN DX=1 ELSE IF DX<-1 THEN DX=-1
3020 DY=FNRAND(20)/20
3030 IF DY>1 THEN DY=1 ELSE IF DY<-1 THEN DY=-1
3040 PEX=EX:PEY=EY:EX=EX+DX:EY=EY+DY
3050 LOCATE PEY,PEX:PRINT" ";
3060 IF PEY>22 THEN FE=0:EX=FNRAND(40):EY=1:EL=EL-1:IF EL>0 THEN 3190 ELSE 4500
3070 IF EX>40 THEN EX=40
3080 IF EX<1 THEN EX=1
3090 IF EY<1 THEN EY=1
3100 IF SCREEN(EY,EX)<>32 THEN EY=PEY:EX=PEX
3180 COLOR 4,0:LOCATE EY,EX:PRINT"#";:COLOR 7,0
3190 RETURN
3999 ' Enemy explode
4000 FOR I=1 TO 30
4010 COLOR FNRAND(16),FNRAND(8):LOCATE EY,EX:PRINT CHR$(FNRAND(128)+127);
4020 NEXT:COLOR 7,0
4030 EX=FNRAND(40):EY=1
4050 FE=0:RETURN
4499 ' You're gone!
4500 FOR I=1 TO 50
4510 LOCATE CY,CX:COLOR FNRAND(16),FNRAND(8):PRINT CHR$(FNRAND(128)+127)
4520 NEXT
4530 COLOR 7,0,0:CLS:FOR I=1 TO 15:A$=INKEY$:NEXT
4550 END
4999 ' Mother ship
5000 CLS:LOCATE 1,1,0
5100 PRINT"              ***********
5110 PRINT"             кФФФФФФФФФФФП
5120 PRINT"             УФФФФФФФФФФФД
5130 PRINT"             УФФФФФФФФФФФД
5140 PRINT"             ГOOOOOOOOOOOГ
5150 PRINT"             ллллллллллллл
5160 PRINT"             ллллллллллллл
5170 PRINT"             ллллллллллллл
5180 ' Y=9
5190 CY=22:CX=1:P=15
5300 A$=INKEY$:PY=CY:PX=CX
5305 IF LEN(A$)=2 THEN R$=RIGHT$(A$,1) ELSE R$=""
5310 IF R$="K"THEN CX=CX-1:IF CX<1 THEN CX=1
5320 IF R$="M"THEN CX=CX+1:IF CX>40 THEN CX=40
5360 IF A$=" "THEN GOSUB 2000:GOTO 5500
5370 LOCATE PY,PX:PRINT" ";
5380 LOCATE CY,CX:PRINT"^";
5390 IF FNRAND(P)=1 THEN GOSUB 6000
5400 GOTO 5300
5500 IF F1=1 THEN P=P-3:IF P<1 THEN P=1
5505 IF F1=1 THEN F1=0:SC=SC+10:GOTO 5300
5510 IF F2=1 THEN F2=0:SC=SC+500:EL=EL+2 ELSE 5300
5520 SCREEN 1:FOR I=1 TO 160:COLOR I MOD 8:NEXT
5530 SCREEN 0:COLOR 7,0:CLS:LOCATE 1,1,0
5540 RETURN
5999 ' Mother fire
6000 MX=FNRAND(5)+CX-3:IF MX<14 THEN MX=14 ELSE IF MX>26 THEN MX=26
6010 FOR Y=10 TO 23
6020 LOCATE Y-1,MX:PRINT" ";
6030 LOCATE Y,MX:PRINT"К";
6040 NEXT:LOCATE Y-1,MX:PRINT" ";
6050 IF MX=CX THEN RETURN 4500
6060 RETURN
