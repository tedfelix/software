1 ' Gold Mine v2.00
2 ' Copyright 2017, Ted Felix.  File date: 8/15/1984 12:08:30 AM
3 ' Random function and randomness
4 RANDOMIZE VAL(TIME$)+VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
5 DEF FNRAND(X)=INT(RND*X)+1:KEY OFF
6 PLAY"mf"
9 ' Title screen
10 SCREEN 0,0,0:WIDTH 40:COLOR 7,0
20 CLS:LOCATE ,,0:PRINT TAB(16);"Gold Mine"
25 PRINT TAB(14);"By: Ted Felix"
30 PRINT:PRINT:PRINT"Instructions (Y/N)?";
40 A$=INKEY$:IF A$=""THEN 40
50 IF A$="Y"OR A$="y"THEN GOSUB 9000
59 ' Set keypad in Num-Lock state
60 DEF SEG=&H40:POKE &H17,176
69 ' Detect monitor
70 CLS:PRINT"T":DEF SEG=&HB000
80 IF PEEK(0)=84 THEN CLS:PRINT"W":IF PEEK(0)=87 THEN MONO=1:COLR=0 ELSE COLR=1:MONO=0 ELSE COLR=1:MONO=0
90 CLS
99 ' Set-up strings
100 A$=STRING$(39,177):E$=" Ä Ä ":E1$=" Ä³Ä ":E2$="ÛÛÛ":S$=CHR$(177)
110 B$=S$+STRING$(32,219)+E$+S$
120 C$=S$+STRING$(28,219)+SPACE$(4)+E$+S$
130 EL$=" "+CHR$(4)+" ":MI$=CHR$(1)
140 ME$(1)="Sandstone, easy digging."
150 ME$(2)="Solid granite, try drill."
160 ME$(3)="Unstable footing, watch out!"
165 ME$(4)="Cave-in!":ME$(5)="Cave-in, you lost your gold!"
170 ME$(6)="Underground spring, flooding."
175 ME$(7)="Fuse went out! Cheap stuff."
180 ME$(8)="Need some room to get away!"
189 ' Set-up numeric variables
190 START=1:MCO=0:MAXP=250000!
200 DO=1000:GOSUB 4000
210 X=34:Y=2:PX=X:PY=Y:LOCATE Y,X:PRINT MI$;
220 A$=INKEY$:IF A$=""THEN 220
230 IF A$="6"THEN E=0:X=35:GOTO 2000
240 GOTO 220
999 ' Main move, screen check... routine
1000 COLOR 2,0:LOCATE Y,X:PRINT MI$;
1001 START=0:A$=INKEY$:IF A$<>""THEN 1002 ELSE 1001
1002 COLOR 7,0:LOCATE Y,X:PRINT MI$;
1003 GOSUB 3500:COLOR 0,7:LOCATE 21,2:PRINT SPACE$(32);:COLOR 7,0
1005 FL=0:PY=Y:PX=X:LOCATE 1,19:COLOR 0,7:PRINT DO;:COLOR 7,0
1009 ' Check input
1010 IF A$="4"THEN X=X-1:FL=1:P$=A$
1020 IF A$="6"THEN X=X+1:FL=1:P$=A$
1030 IF A$="8"THEN Y=Y-1:FL=1:P$=A$
1040 IF A$="2"THEN Y=Y+1:FL=1:P$=A$
1050 IF A$="P"OR A$="p"THEN 6000
1060 IF A$="D"OR A$="d"THEN 6100
1070 IF A$="E"OR A$="e"THEN 6200
1480 IF A$="Q"OR A$="q"THEN CLS:GOTO 8500
1490 IF FL=0 THEN 1000
1499 ' Stay within mine
1500 IF X<2 THEN X=2:GOTO 1000
1510 IF X>34 AND Y=YL THEN 2000 ELSE IF X>34 THEN X=34:GOTO 1000
1520 IF Y<4 THEN Y=4:GOTO 1000
1530 IF Y>20 THEN Y=20:GOTO 1000
1535 SC=SCREEN(Y,X)
1539 ' What did you land on?
1540 IF SC=219 THEN 1900
1550 IF SC=32 THEN 1800
1560 IF SC=249 THEN 5000
1570 IF SC=177 THEN 5300
1580 IF SC=254 THEN 5500
1590 IF SC=83 THEN 5600
1600 IF SC=247 THEN X=PX:Y=PY:GOTO 1000
1790 GOTO 1000
1799 ' Just run around spaces
1800 LOCATE PY,PX:PRINT" ";:LOCATE Y,X:PRINT MI$;:GOTO 1000
1899 ' Pick-axe through rock
1900 COLOR 7,0:DO=DO-5:IF DO<1 THEN 8710
1905 COLOR 0,7:LOCATE 1,19:PRINT DO;:COLOR 7,0
1910 FOR I=0 TO 10:LOCATE Y,X:PRINT CHR$(24);
1920 FOR DE=0 TO 100:NEXT:LOCATE Y,X
1930 PRINT CHR$(27);:FOR DE=1 TO 100:NEXT
1940 NEXT:LOCATE PY,PX:PRINT" ";:LOCATE Y,X:PRINT MI$;:GOTO 1000
1999 ' Elevator
2000 LOCATE PY,PX:PRINT" ";
2010 A$=INKEY$:IF A$=""THEN 2010
2015 IF START=1 THEN A=1 ELSE A=2
2020 IF A$="2"THEN 2200
2030 IF A$="8"THEN 2300
2040 IF A$="4"THEN X=35:START=0:ON A GOTO 3000,1000
2070 GOTO 2010
2199 ' Elevator down
2200 FOR Y1=Y TO 18:YL=Y1:A$=INKEY$
2210 IF A$<>""THEN Y1=23:X=35:GOTO 2290
2220 LOCATE Y1-1,34:COLOR 7,0:PRINT E1$;
2225 LOCATE Y1,35:PRINT E2$;
2230 LOCATE Y1+1,35:COLOR 20,7:PRINT EL$;
2240 COLOR 7,0:LOCATE Y1+2,35:PRINT E2$;
2245 SOUND 220-Y1*5+100,5:Y=Y1+1
2290 NEXT:IF A$="8"THEN 2300
2294 IF Y1=19 THEN YL=YL+1
2296 GOTO 1000
2299 ' Elevator up
2300 FOR Y1=Y TO 2 STEP-1:YL=Y1+1:A$=INKEY$
2310 IF A$<>""THEN Y1=0:X=35:GOTO 2390
2315 IF Y1+2=21 THEN LOCATE 21,34:COLOR 7,0:PRINT" ÁÁÁ ";:GOTO 2325
2320 LOCATE Y1+2,34:PRINT E$;
2325 LOCATE Y1+1,35:PRINT E2$;
2330 LOCATE Y1,35:COLOR 20,7:PRINT EL$
2340 COLOR 7,0:LOCATE Y1-1,35:PRINT E2$;
2345 SOUND 220-Y1*5+100,5:Y=Y1
2390 NEXT:IF A$="2"THEN 2200 ELSE IF Y1>0 THEN 3000 ELSE 1000
2999 ' Cash-in gold
3000 FOR I=34 TO 25 STEP-1
3010 LOCATE 2,I:PRINT" ";:LOCATE 2,I-1
3020 PRINT MI$;
3030 FOR DE=1 TO 200:NEXT
3040 NEXT
3100 M=GOLD*CP:LOCATE 21,2:COLOR 0,7
3110 PRINT GOLD"Oz. of gold @ "USING"$###";CP;:PRINT" = "USING"$#####";M;
3120 DO=DO+M:GOLD=0:COLOR 7,0
3130 COLOR 0,7:LOCATE 1,19:PRINT DO;:COLOR 7,0
3140 A$=INKEY$:IF A$=""THEN 3140
3150 IF A$="6"THEN 3210
3160 IF A$="4"THEN GOSUB 10000 ELSE 3140
3210 FOR I=25 TO 34
3220 LOCATE 2,I-1:PRINT" ";:LOCATE 2,I:PRINT MI$;
3230 FOR DE=1 TO 200:NEXT:NEXT
3240 IF DO>MAXP THEN 8000
3250 LOCATE 2,34:PRINT" ";:GOTO 2000
3499 ' Change gold price
3500 CP=CP+FNRAND(41)-21:IF CP<200 THEN CP=CP+FNRAND(20)
3510 LOCATE 1,10:COLOR 0,7:PRINT CP;
3520 LOCATE 2,10:COLOR 6,0:PRINT"GOLD";
3530 COLOR 7,0
3540 MCO=MCO+1:IF MCO>=100 THEN 3550
3545 IF MCO>9 AND MCO<11 THEN LOCATE 23,1:PRINT SPACE$(39);:RETURN ELSE RETURN
3549 ' Earn interest on money in bank
3550 MCO=0:INTRATE=(FNRAND(5)+5)/100
3560 INTEREST=INT(DO*INTRATE)
3570 DO=DO+INTEREST:IF DO>=MAXP THEN 8000
3580 LOCATE 23,1:PRINT USING"$###";INTEREST;:PRINT" interest at %";USING"##";INTRATE*100;:PRINT" interest.":COLOR 7,0
3590 RETURN
3999 ' Set-up mine on screen
4000 CLS:LOCATE 3:PRINT A$:LOCATE 3,34:PRINT E$
4030 FOR I=1 TO 3
4040 PRINT B$:PRINT B$:PRINT C$:PRINT C$:PRINT C$:PRINT B$:NEXT:PRINT A$
4050 FOR I=0 TO 200
4060 Y=FNRAND(17)+3:X=FNRAND(28)+1
4070 LOCATE Y,X:COLOR 0,7:PRINT"ù";:NEXT
4080 LOCATE 1,1:PRINT"FELIX";:LOCATE 2,1:PRINT"MINE";
4090 LOCATE 1,19:PRINT DO;:LOCATE 2,20:PRINT"BANK";
4095 LOCATE 1,35:PRINT"   ";:LOCATE 2,35:COLOR 20,7:PRINT EL$;:COLOR 0,7:LOCATE 3,35:PRINT"   ";
4100 CP=FNRAND(200)+300
4110 LOCATE 1,10:PRINT CP;:LOCATE 2,10
4120 COLOR 6,0:PRINT"GOLD";:COLOR 7,0
4130 LOCATE 21,34:PRINT" ÁÁÁ ";
4200 RETURN
4999 ' What's under it?
5000 A=FNRAND(5)+1
5010 IF FNRAND(5)=1 THEN A=1
5020 ON A GOTO 5100,5200,5300,5400,5500,5600
5030 CLS:PRINT"PARITY CHECK 2"
5040 FOR I=0 TO 5000:NEXT
5050 CLS:PRINT"just joking!":END
5099 ' Found gold!
5100 LOCATE 21,2:OZ=FNRAND(4)
5105 COLOR 0,7:PRINT"Found gold nugget"OZ"Oz.";:COLOR 7,0
5110 GOLD=GOLD+OZ
5115 LOCATE PY,PX:PRINT" ";:LOCATE Y,X
5120 PRINT MI$;
5125 FOR I=700 TO 900 STEP 5:SOUND I,.1:NEXT
5130 GOTO 1000
5199 ' Sandstone
5200 M.NUM=1:GOSUB 7000
5210 LOCATE PY,PX:PRINT" ";:LOCATE Y,X
5220 PRINT MI$;:GOTO 1000
5299 ' Granite
5300 M.NUM=2:GOSUB 7000:GRAN$=CHR$(177)
5305 IF COLR=1 THEN COLOR 7,4:LOCATE Y,X:PRINT GRAN$; ELSE COLOR 7,0:LOCATE Y,X:PRINT GRAN$;
5310 X=PX:Y=PY:GOTO 1000
5399 ' Unstable footing
5400 M.NUM=3:GOSUB 7000
5405 FOR I=900 TO 600 STEP-5:SOUND I,.1:NEXT
5410 LOCATE Y,X:PRINT" ";:LOCATE PY,PX
5420 PRINT" ";:Y=Y+1:IF Y>21 THEN Y=21
5430 LOCATE Y,X:PRINT MI$;:GOTO 1000
5499 ' Cave-in
5500 M.NUM=4:GOSUB 7000:LOCATE Y,X:PRINT"þ";:X=PX:Y=PY
5505 FOR I=0 TO 50
5510 X2=FNRAND(10)+X-5:Y2=FNRAND(10)+Y-5
5515 IF X2<2 OR X2>30 OR Y2<4 OR Y2>22 THEN 5535
5520 SC=SCREEN(Y2,X2):IF SC<>32 AND SC<>249 THEN 5535
5525 SOUND 1000,.1
5530 LOCATE Y2,X2:PRINT"Û";
5535 NEXT
5540 IF FNRAND(3)=1 AND GOLD>0 THEN M.NUM=5:GOSUB 7000:GOLD=0
5590 GOTO 1000
5599 ' Underground spring
5600 M.NUM=6:GOSUB 7000:LOCATE Y,X:COLOR 0,7:PRINT"S";:COLOR 7,0:NY=Y:X=PX:Y=PY
5610 FOR Y2=NY TO 20:FOR X2=2 TO 33
5615 SC=SCREEN(Y2,X2)
5620 IF SC=32 OR SC=247 THEN LOCATE Y2,X2:PRINT"÷";:F1=1:SOUND 3000,.1
5625 NEXT:IF F1=0 THEN Y2=24
5630 F1=0:NEXT
5690 GOTO 1000
5998 ' Other moves besides pick-axe:
5999 ' Pump
6000 Y=PY:X=PX:IF P$="8"THEN Y=Y-1:IF Y<4 THEN Y=4:GOTO 1000
6010 IF P$="2"THEN Y=Y+1:IF Y>20 THEN Y=20:GOTO 1000
6020 IF P$="4"THEN X=X-1:IF X<2 THEN X=2:GOTO 1000
6030 IF P$="6"THEN X=X+1:IF X>33 THEN X=33:LOCATE PY,PX:PRINT" ";:GOTO 1000
6035 IF SCREEN(Y,X)<>247 THEN Y=PY:X=PX:GOTO 1000
6040 FOR I=1 TO 8:LOCATE Y,X:PRINT"ï";
6050 SOUND 500,.1:FOR DE=1 TO 50:NEXT:LOCATE Y,X
6060 SOUND 500,.1:PRINT"U";:FOR DE=1 TO 50:NEXT:NEXT
6070 LOCATE Y,X:PRINT MI$:LOCATE PY,PX:PRINT" ";
6080 DO=DO-10:IF DO<=0 THEN 8710 ELSE 1000
6099 ' Drill
6100 Y=PY:X=PX:IF P$="8"THEN Y=Y-1:IF Y<4 THEN Y=4:GOTO 1000
6110 IF P$="2"THEN Y=Y+1:IF Y>20 THEN Y=20:GOTO 1000
6120 IF P$="4"THEN X=X-1:IF X<2 THEN X=2:GOTO 1000
6130 IF P$="6"THEN X=X+1:IF X>33 THEN 1000
6135 IF SCREEN(Y,X)<>177 AND SCREEN(Y,X)<>219 THEN Y=PY:X=PX:GOTO 1000
6140 FOR I=1 TO 8:LOCATE Y,X:PRINT"<";
6145 SOUND 100,.1
6150 FOR DE=1 TO 20:NEXT:LOCATE Y,X
6155 SOUND 100,.1
6160 PRINT"=";:FOR DE=1 TO 50:NEXT:NEXT
6170 LOCATE Y,X:PRINT MI$:LOCATE PY,PX:PRINT" ";
6180 DO=DO-30:IF DO<=0 THEN 8710 ELSE GOTO 1000
6199 ' Explode (Set dynamite charge)
6200 IF SCREEN(Y,X+1)<>32 THEN LOCATE 23,1:M.NUM=8:GOSUB 7000:GOTO 1000
6210 LOCATE Y,X:PRINT"|";:EX=X:EY=Y:X=X+1
6220 PX=X:X=X+1:IF SCREEN(Y,X)<>32 THEN 6240
6225 FOR DE=1 TO 50:NEXT:LOCATE Y,PX:PRINT" ";
6230 LOCATE Y,X:PRINT MI$;:GOTO 6220
6240 X=X-1:FOR I=1 TO 20:LOCATE EY,EX:PRINT"X";
6242 SOUND 2000,1
6245 LOCATE EY,EX:PRINT"|";:NEXT
6250 IF FNRAND(3)=1 THEN LOCATE 23,1:M.NUM=7:GOSUB 7000:LOCATE EY,EX:PRINT" ";:DO=DO-45:IF DO<=0 THEN 8710 ELSE 1000
6252 FOR I=400 TO 100 STEP-5:SOUND I,.1:NEXT:LOCATE EY,EX:PRINT" ";
6255 FOR I=1 TO 100
6258 X3=FNRAND(10)-5+EX
6260 Y3=FNRAND(10)-5+EY
6262 IF X3>30 OR X3<2 OR Y3>20 OR Y3<4 THEN 6258
6265 LOCATE Y3,X3:PRINT" ";
6270 NEXT
6280 DO=DO-45:IF DO<=0 THEN 8710 ELSE 1000
6999 ' Display message #m.num
7000 LOCATE 21,3:COLOR 0,7:PRINT ME$(M.NUM);:COLOR 7,0:RETURN
7999 ' You Won!
8000 CLS:PRINT"Congratulations!! You ended up with"
8010 PRINT DO;"dollars!!!!"
8500 PRINT"Play Again (Y/N)";
8505 :FOR I=0 TO 100:A$=INKEY$:NEXT
8510 A$=INKEY$:IF A$=""THEN 8510
8520 IF A$="Y"OR A$="y"THEN 3
8530 DEF SEG=&H40:POKE &H17,144
8540 END
8700 ' You lose!!
8710 CLS:PRINT:PRINT"You've gone broke!!"
8720 GOTO 8500
8999 ' Instructions
9000 LOCATE 5,1:PRINT"Controls:"SPACE$(20)
9010 PRINT"8 - Up":PRINT"2 - Down":PRINT"4 - Left":PRINT"6 - Right"
9020 PRINT"P - Pump":PRINT"D - Drill":PRINT"E - `Explode': Set dynamite charge"
9030 PRINT"Q - Quit"
9040 PRINT:PRINT"Use the numeric keypad in NUMLOCK state for directional control."
9050 PRINT:INPUT"Press ENTER to continue";A
9060 CLS:PRINT"Gold mine object and game rules:"
9070 PRINT:PRINT"The object of gold mine is to find      $10,000 worth of gold in the mine."
9080 PRINT:PRINT"The elevator on the right hand side of  the screen is used to move up and down  or to cash-in your gold.  You can carry an unlimited amount of gold, but some   hazards will cause you to lose your     gold.  The bank changes depending on
9085 PRINT"what moving method you use (Drill,      pump or pick-axe)"
9090 PRINT:PRINT"Dots are placed throughout the mine.    These dots have either gold or some     hazard under them.  The dots are the    only places where you will find gold.
9200 LOCATE 22,1:INPUT"Press ENTER to play";A$:RETURN
9999 ' Banking
10000 SCREEN ,,4:CLS:DO$="$#######,"
10010 PRINT MI$;" Gold Mine "MI$" - Banking Mode"
10020 COLOR 23,0:PRINT:PRINT"Felix Bank";:COLOR 7,0
10030 PRINT:PRINT"1> Stocks"
10040 PRINT"2> Gold"
10050 PRINT"3> Money market funds"
10100 PRINT"Selection ";:COLOR 23,0:PRINT MI$;
10110 A$=INKEY$:IF A$=""THEN 10110
10120 IF A$="1" THEN 11000
10130 IF A$="2" THEN 13000
10140 IF A$="3" THEN 15000
10150 IF A$="Q"OR A$="q" THEN SCREEN ,,0:COLOR 7,0:A$="":RETURN
10160 GOTO 10110
10999 ' Stocks
11000 ST$(1)="IBM":ST(1)=FNRAND(40)+40
11010 ST$(2)="TFS":ST(2)=FNRAND(10)+20
11020 ST$(3)="ConPowr":ST(3)=FNRAND(30)+20
11030 ST$(4)="Kmart":ST(4)=FNRAND(10)+10
11040 ST$(5)="Mine Equip.":ST(5)=FNRAND(20)+30
11060 CLS:COLOR 7,0
11070 PRINT">> Stocks <<"
11080 FOR I=1 TO 5
11090 PRINT ST$(I)TAB(15)USING"$###";ST(I)
11100 NEXT
11110 N=0:FOR I=1 TO 5:IF SB(I)>0 THEN N=N+1
11120 NEXT
11130 IF N=0 THEN PRINT"You have no stocks.":GOTO 11200
11140 PRINT:PRINT"Stocks bought:"
11150 FOR I=1 TO 5
11160 IF SB(I)>0 THEN PRINT ST$(I)TAB(15)USING"#### Shares ";SB(I);:PRINT USING"$#####";SB(I)*ST(I)
11170 NEXT
11200 PRINT:PRINT"You have "USING DO$;DO:PRINT"Would you like to:"
11210 PRINT"(B)uy or (S)ell stock?"
11220 COLOR 23,0:PRINT MI$;:COLOR 7,0
11230 A$=INKEY$:IF A$=""THEN 11230
11240 IF A$="B"OR A$="b"THEN 11500
11250 IF A$="S"OR A$="s"THEN 12000
11260 IF A$="Q"OR A$="q"THEN 10000
11499 ' Buy stock
11500 GOSUB 19000
11545 PRINT:PRINT"You have "USING DO$;DO
11550 PRINT:INPUT"Which stock would you like to buy";ST
11560 IF ST<=0 THEN 11000 ELSE IF ST>5 THEN 11550
11570 INPUT"How many shares";SH
11580 IF SH<0 THEN 11570
11590 COST=SH*ST(ST)
11600 IF COST>DO THEN PRINT"You don't have enough money!":GOTO 11545
11610 PRINT"That will cost you"USING DO$;COST
11620 INPUT"Are you sure";A$
11630 A$=LEFT$(A$,1)
11640 IF A$="n"OR A$="N"THEN 11500
11650 IF A$="y"OR A$="Y"THEN 11670
11660 GOTO 11620
11670 DO=DO-COST:SB(ST)=SB(ST)+SH
11680 GOTO 11500
11999 ' Sell stock
12000 GOSUB 19000
12010 PRINT:PRINT"You have "USING DO$;DO
12020 PRINT:INPUT"Which stock would you like to sell";ST
12030 IF ST<=0 THEN 11000 ELSE IF ST>5 THEN 12020
12040 INPUT"How many shares";SH
12050 IF SH>SB(ST)OR SH<0 THEN 12040
12060 NET=SH*ST(ST)
12070 PRINT"That will give you"USING DO$;NET
12080 DO=DO+NET
12090 SB(ST)=SB(ST)-SH
12100 FOR I=0 TO 2000:NEXT:GOTO 12000
12999 ' Gold
13000 COLOR 7,0:CLS:PRINT"Gold price "USING"$###";CP
13010 PRINT"You have "USING"###";G1;:PRINT" Oz. of gold"
13020 PRINT"You have "USING DO$;DO
13030 PRINT:PRINT"Would you like to (B)uy or (S)ell?"
13040 A$=INKEY$:IF A$=""THEN 13040
13050 IF A$="B"OR A$="b"THEN 13100
13060 IF A$="S"OR A$="s"THEN 13500
13070 IF A$="Q"OR A$="q"THEN 10000
13080 GOTO 13040
13099 ' Buy Gold
13100 CLS:PRINT"You have "USING DO$;DO
13110 PRINT"Gold is selling for "USING"$###";CP
13120 PRINT:INPUT"How many ounces would you like to buy   ";GBUY
13130 IF GBUY>9999 OR GBUY<0 THEN 13120
13140 COST=GBUY*CP
13150 IF COST>DO THEN PRINT"You don't have enough money":GOTO 13120
13160 DO=DO-COST:G1=G1+GBUY
13170 GOTO 13000
13499 ' Sell Gold
13500 CLS:PRINT"You have "USING"####";G1;
13510 PRINT" Oz. of gold."
13520 PRINT"Gold is selling for "USING"$###";CP
13530 PRINT:INPUT"How much gold would you like to sell    ";GSEL
13540 IF GSEL>G1 THEN PRINT"You don't have that much gold.":GOTO 13530
13550 IF GSEL<0 THEN 13530
13560 NET=GSEL*CP
13570 DO=DO+NET
13580 PRINT"You made "USING"$#####";NET
13590 FOR I=0 TO 2000:NEXT:GOTO 13000
14999 ' Money market funds
18999 ' Display stocks
19000 CLS:PRINT"Stock"TAB(20)"Price"TAB(32)"#"
19010 FOR I=1 TO 5
19020 PRINT I;ST$(I)TAB(20)USING"$###";ST(I);
19030 PRINT TAB(30)USING"####";SB(I)
19040 NEXT
19050 RETURN

