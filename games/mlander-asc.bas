1 ' Copyright 2017, Ted Felix
2 ' File date: 12/31/1986 02∶17∶56 PM
10 RANDOMIZE VAL(TIME$)+VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
20 DEF FNRAND(X)=INT(RND*X)+1
30 SCREEN 0:WIDTH 80:CLS:PRINT"Monochrome Lander":PRINT"By: Ted Felix"
37 FOR I=1 TO 3000:NEXT
40 U$=CHR$(30)+"/":D$="\"+CHR$(31):LANDER$=CHR$(127)
50 GROUND$="/":GOSUB 2000
100 CLS:IF SGN(BDOWN)=1 THEN LOCATE 23-BDOWN,1:PRINT GROUND$
110 IF SGN(BDOWN)<>1 THEN LOCATE 22:PRINT GROUND$
120 GRAVITY=.01:AIR.RESIS=.01:X=3:Y=1:VX=.2:VY=.5
200 'DEF SEG=&H40:Z=PEEK(&H17):IF (Z AND &H20)=0 THEN POKE &H17,Z OR &H20
1000 A$=INKEY$:A$=RIGHT$(A$,1)
1010 IF A$="H" THEN VY=VY-.1
1020 IF A$="P" THEN VY=VY+.1
1030 IF A$="K" THEN VX=VX-.1
1040 IF A$="M" THEN VX=VX+.1
1050 VY=VY+GRAVITY:IF SGN(INT(VX*100))=1 THEN VX=VX-AIR.RESIS ELSE IF SGN(INT(VX*100))=-1 THEN VX=VX+AIR.RESIS
1060 PX=X:PY=Y:X=X+VX:Y=Y+VY
1070 IF Y>23 THEN Y=23 ELSE IF Y<1 THEN VY=-VY:Y=1
1080 IF X<1 THEN VX=-VX:X=1 ELSE IF X>79 THEN X=79:VX=-VX
1085 LOCATE PY,PX:PRINT" ";
1090 SC=SCREEN(Y,X):IF SC=32 THEN 1180
1100 IF SC=126 THEN 3000
1170 GOTO 3500
1180 LOCATE Y,X:PRINT LANDER$;:LOCATE 24,1:PRINT"Y Veloc."INT(VY*100)TAB(20)"X Veloc."INT(VX*100);
1190 GOTO 1000
1999 ' Make surface
2000 FOR I=1 TO 79:A=FNRAND(3)
2010 IF A=1 THEN GROUND$=GROUND$+U$:DOWN=DOWN-1
2020 IF A=2 THEN GROUND$=GROUND$+D$:DOWN=DOWN+1
2030 IF A=3 THEN GROUND$=GROUND$+"~"
2035 IF DOWN>BDOWN THEN BDOWN=DOWN
2040 NEXT:RETURN
2998 ' Messages
2999 ' Landed on a flat space
3000 IF VX>.4 OR VX<-.4 OR VY>.8 THEN 4000
3010 LOCATE Y-1,X:PRINT LANDER$
3020 LOCATE 1,1:PRINT"Nice landing!"
3030 PRINT"Velocity on impact:";INT(VY*100)
3040 GOTO 5000
3499 ' Landed on slant
3500 GOTO 4000
3999 ' Crash!!
4000 CLS:PRINT"Crash"
5000 FOR I=0 TO 450:A$=INKEY$:NEXT:COLOR 15:PRINT"Play Again (Y/N)?":COLOR 7:A$=INPUT$(1):IF A$="Y"OR A$="y"THEN RUN
5010 DEF SEG=&H40:POKE &H17,Z
5020 CLS:PRINT"Later

