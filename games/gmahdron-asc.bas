1 RANDOMIZE TIMER
2 ' Copyright 2017, Ted Felix
3 ' File date: 5/13/1984 03:27:02 PM
5 DEF FNRND1(X)=INT(RND*X)+1
10 CLS:COLOR 7,0:WIDTH 40':FOR I=1 TO 23:PRINT SPACE$(40);:NEXT
15 GOSUB 5000
20 TH=.2:DE=.01   ' THrust, DEcay
100 EX=5:EY=10:EDX=FNRND1(3)-2:EDY=FNRND1(3)-2:IF EDX=0 OR EDY=0 THEN 100
200 X=35:Y=10:PX=X:PY=Y
1000 A$=INKEY$:IF A$=""THEN 1100
1010 IF A$="8"THEN DY=DY-TH
1020 IF A$="2"THEN DY=DY+TH
1030 IF A$="4"THEN DX=DX-TH
1040 IF A$="6"THEN DX=DX+TH
1050 IF A$=" "THEN GOSUB 2000
1060 FOR I=1 TO 5:A$=INKEY$:NEXT:LOCATE PY,PX:PRINT" ";
1100 C$="*":PX=X:PY=Y:X=X+DX:Y=Y+DY
1120 IF X<1 THEN X=1:DX=-DX:C$="|"
1130 IF X>39 THEN X=39:DX=-DX:C$="|"
1140 IF Y<1 THEN Y=1:DY=-DY:C$="-"
1150 IF Y>23 THEN Y=23:DY=-DY:C$="-"
1160 IF DX<9.999999E-05 AND DX>-9.999999E-05 THEN DX=0
1170 IF DY<9.999999E-05 AND DY>-9.999999E-05 THEN DY=0
1172 IF DX>1 THEN DX=1 ELSE IF DX<-1 THEN DX=-1
1174 IF DY>1 THEN DY=1 ELSE IF DY<-1 THEN DY=-1
1180 IF SCREEN(Y,X)=45 THEN DY=-DY:Y=PY:X=PX
1185 IF SCREEN(Y,X)=124 THEN DX=-DX:Y=PY:X=PX
1190 DX=DX-SGN(DX)*DE:DY=DY-SGN(DY)*DE:LOCATE PY,PX:PRINT" ";:LOCATE Y,X:PRINT C$;
1499 ' enemy move
1500 '
1510 IF FNRND1(10)=1 THEN GOSUB 4000
1550 PEX=EX:PEY=EY:EX=EX+EDX:EY=EY+EDY
1560 IF EX>38 THEN EDX=-EDX:EX=38
1565 IF EX<2 THEN EDX=-EDX:EX=2
1570 IF EY>23 THEN EDY=-EDY:EY=23
1572 IF EY<2 THEN EDY=-EDY:EY=2
1573 S1=SCREEN(EY,EX)
1575 IF S1<>32 AND S1<>219 THEN EDY=-EDY:EDX=-EDX:EX=EX+EDX:EY=EY+EDY:PEY=EY:PEX=EX
1580 LOCATE PEY,PEX:PRINT" ";:LOCATE EY,EX:PRINT"O";
1590 EDX=EDX+(FNRND1(3)-2)/4:IF EDX>1 THEN EDX=1 ELSE IF EDX<-1 THEN EDX=-1
1595 EDY=EDY+(FNRND1(3)-2)/4:IF EDY>1 THEN EDY=1 ELSE IF EDY<-1 THEN EDY=-1
1600 GOTO 1000
1999 ' FIRE!
2000 FX=X+SGN(DX):FY=Y+SGN(DY):FDX=SGN(DX):FDY=SGN(DY):FOR F=1 TO 10
2005 S1=SCREEN(FY,FX)
2010 IF S1=79 THEN 3000
2015 IF S1<>32 AND S1<>219 THEN F1=F:F=100:GOTO 2050
2020 IF FX>39 OR FX<2 THEN F1=F:F=10:GOTO 2050
2030 IF FY>23 OR FY<2 THEN F1=F:F=10:GOTO 2050
2040 LOCATE FY,FX:COLOR 4,0:PRINT"л";:COLOR 7,0:FX=FX+FDX:FY=FY+FDY
2050 NEXT:IF F>90 THEN F=F1
2200 FX=X+SGN(DX):FY=Y+SGN(DY):FOR F=1 TO F-1
2210 IF FX>39 OR FX<2 THEN RETURN
2220 IF FY>23 OR FY<2 THEN RETURN
2230 LOCATE FY,FX:PRINT" ";:FY=FY+FDY:FX=FX+FDX:NEXT:RETURN
3000 LOCATE 1,1:PRINT"YAAAAHHHAAHAHAHHHAH":SC=SC+1:LOCATE 7,12:PRINT"Score:"SC
3010 FOR I=1 TO 2000:NEXT:CLS:GOTO 10
3999 ' Enemy fire
4000 IF EX<2 OR EY<2 OR EX>38 OR EY>22 THEN RETURN
4005 COLOR 23,0:LOCATE EY-1,EX-1:PRINT"ллл";
4010 LOCATE EY,EX-1:PRINT"лOл";
4020 LOCATE EY+1,EX-1:PRINT"ллл";
4030 IF EY>3 AND EX>3 AND EX<37 AND EY<21 THEN 4050                  |
4040 GOTO 4200
4050 LOCATE EY-2,EX-2:PRINT"ллллл";
4060 LOCATE EY-1,EX-2:PRINT"ллллл";
4070 LOCATE EY,EX-2:PRINT"ллOлл";
4080 LOCATE EY+1,EX-2:PRINT"ллллл";
4090 LOCATE EY+2,EX-2:PRINT"ллллл";
4100 IF EY>4 AND EX>4 AND EX<36 AND EY<20 THEN 4120
4110 GOTO 4200
4120 LOCATE EY-3,EX-3:PRINT"ллллллл";
4125 LOCATE EY-2,EX-3:PRINT"ллллллл";
4126 LOCATE EY-1,EX-3:PRINT"ллллллл";
4130 LOCATE EY,EX-3:PRINT"лллOллл";
4140 LOCATE EY+1,EX-3:PRINT"ллллллл";
4150 LOCATE EY+2,EX-3:PRINT"ллллллл";
4160 LOCATE EY+3,EX-3:PRINT"ллллллл";
4200 LOCATE 14,12:PRINT"   Radiation"
4205 LOCATE 15,12:PRINT"     Alert"
4210 COLOR 7,0:RETURN
4999 ' Gama Hedron
5000 LOCATE 5,10:PRINT STRING$(20,"-")
5010 FOR I=6 TO 15:PRINT TAB(10)"|"SPACE$(18)"|":NEXT
5020 LOCATE 16,10:PRINT STRING$(20,"-")
5030 LOCATE 7,12:PRINT"Score:";SC
5040 LOCATE 9,12:PRINT"  Gamma Hedron"
5050 LOCATE 10,12:PRINT"   Masterbyte"
5055 LOCATE 11,12:PRINT"    Software"
5060 RETURN
