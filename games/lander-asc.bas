1 ' Mars Lander
2 RANDOMIZE TIMER
3 ' Copyright 2017, Ted Felix
4 ' File date: 5/7/1989 08:43:06 PM
5 GOSUB 2000:IF FL=0 THEN MX=115:MY=121:RX=462:RY=484
10 STRIG(0)ON:CLS:SCREEN 0:WIDTH 40:COLOR 7,0:KEY OFF:LOCATE 1,1,0
19 ' Display title
20 LOCATE 1,18:PRINT"Mars"
30 LOCATE 2,17:PRINT"Lander"
40 LOCATE 3,14:PRINT"By: Ted Felix"
50 LOCATE 5,10:PRINT"All Time High-Scores:"
59 ' Display high scores
60 OPEN"lander10.dat"FOR INPUT AS #1
70 INPUT #1,A:FOR I=0 TO A-1
80 INPUT #1,A$,S:PRINT TAB(6)A$ TAB(30)S
90 NEXT
100 CLOSE #1
110 PRINT:PRINT"Press the button to continue"
120 IF STRIG(1)=0 THEN COLOR,,(STICK(0)+STICK(1))MOD 16:GOTO 120
129 ' Get skill level
130 CLS
140 PRINT"Which of these is you?:":PRINT
150 PRINT"1   Novice
160 PRINT"2-3 Intermidiate
170 PRINT"4   Pro
180 PRINT"5-9 Expert levels":PRINT:PRINT"The larger the number, the harder the"
190 PRINT"game.":PRINT
195 PRINT"   1  2  3  4  5  6  7  8  9
200 LOCATE 12,X*3+3:PRINT" ";
205 X=STICK(0)/(DX/9):IF INT(X+1.2)>9 THEN X=8.75
210 LOCATE 12,X*3+3:PRINT"^";:LOCATE 13,1:PRINT"Skill ->"INT(X+1.2);
215 IF STRIG(1)=0 THEN COLOR,,(STICK(0)+STICK(1))MOD 16:GOTO 200
220 SKILL=INT(X+1.2):M=1:FUEL=1000
229 ' Set-up screen and lander
230 SCREEN 1:COLOR 0,0:CLS
240 DIM LANDER(9):DEF FNRAND(X)=INT(RND*X)+1
250 GOSUB 1010
260 RANDOMIZE (PEEK(&H46C)+PEEK(&H46D)*256)-32768!
270 COLOR 0,0:CLS:LOCATE 10
279 ' Set-up variables
280 US$="####":U$=CHR$(24)+" ":D$=CHR$(25)+" ":R$=CHR$(26)+" ":L$=CHR$(27)+" "
290 Y=FNRAND(50)+148:PSET(0,Y),2:IF SKILL=2 THEN SKILL=3
300 IF SKILL=2 THEN SKILL=3
309 ' Label Instrumentation
310 LOCATE 19,35:PRINT"Mars";:LOCATE 20,34:PRINT"Lander":LOCATE 22,36:PRINT"TF"
320 LOCATE 7,34:PRINT"M# ";M;
330 LOCATE 8,34:PRINT SC;
340 LOCATE 10,34:PRINT"Skil";SKILL
370 LINE(261,0)-(261,199),2
380 PSET(0,198),2
389 ' Draw terrain
390 FLATFL1=0
392 IF SKILL=1 THEN LINE-(260,Y),2:GOTO 420
395 FOR X=0 TO 260
410 A=FNRAND(SKILL)-((SKILL\2)+1):Y=Y+A:IF Y>198 THEN Y=198
412 IF Y<0 THEN Y=0
414 LINE-(X,Y),2
416 IF RND>.9899999 AND FLATFL1=0 AND X<240 THEN LINE(X,Y)-(X+9,Y),2:X=X+9:FLATFL1=1
418 NEXT
420 LINE(261,0)-(261,199),2
425 PAINT(0,199),2,2:X=20:Y=1:PY=1:PX=20
429 ' Draw stars
430 FOR I=0 TO 50
440 X1=FNRAND(260):Y1=FNRAND(200):IF POINT(X1,Y1)<>0 THEN 440
450 IF FNRAND(2)=1 THEN PSET(X1,Y1)ELSE PSET(X1+1,Y1):PSET(X1-1,Y1):PSET(X1,Y1+1):PSET(X1,Y1-1)
455 NEXT
459 ' Set-up movement factors
460 XV=.6:YV=0:GRAV=.1:RES=.02:PUT(X,Y),LANDER,PSET
464 ' Empty key buffer
465 FOR I=1 TO 16:A$=INKEY$:NEXT
470 ' Lander Routines
480 XT=(STICK(0)-MX)/RX:YT=(STICK(1)-MX)/RX
485 IF ABS(XT)<.05 THEN XT=0 ' Dead space
490 IF FUEL>3 AND STRIG(1)THEN YT=-3:XT=0:XV=0
500 X1=XT*10:Y1=YT*10:FUEL=FUEL-ABS(X1)-ABS(Y1)
510 IF FUEL<=0 THEN XT=0:YT=0:FUEL=0
520 XV=XV+XT-RES*SGN(XV):YV=YV+YT+GRAV
530 X=X+XV:IF X>248 THEN X=248:XV=-XV
540 IF X<1 THEN X=1:XV=-XV
550 Y=Y+YV:IF Y<1 THEN Y=1:YV=-YV
560 PUT(PX,PY),LANDER,XOR
570 IF Y>190 THEN 850
580 IF POINT(X+1,Y+11)=2 THEN IF POINT(X+8,Y+11)<>0 THEN 700 'Ground!
590 IF POINT(X+1,Y+9)=2 THEN IF POINT(X+8,Y+11)=0 THEN 850
600 IF POINT(X+8,Y+9)=2 THEN IF POINT(X+1,Y+11)=0 THEN 850
610 PUT(X,Y),LANDER,XOR:PX=X:PY=Y
620 LOCATE 2,34:IF YV<0 THEN PRINT U$;
630 IF YV>0 THEN PRINT D$;
640 PRINT USING US$;ABS(YV)*100;
650 LOCATE 3,34:IF XV<0 THEN PRINT L$;
660 IF XV>0 THEN PRINT R$;
670 PRINT USING US$;ABS(XV)*100;
680 LOCATE 4,34:PRINT"F ";USING US$;FUEL;
690 GOTO 480
699 ' Ground!
700 IF YV>.75 THEN 850
710 LOCATE 10,8:PRINT"Mission Completed!"
715 IF FLATFL1=0 AND SKILL>7 THEN LOCATE 12,6:PRINT">>> 1000 pt Bonus! <<<"
720 M=M+1
730 PUT(X,Y),LANDER,PSET
740 SKILL=SKILL+1:IF SKILL>9 THEN SKILL=9
750 ADD=INT(X/4)+10+(75-VY*100)+SKILL*10
755 IF FLATFL1=0 AND SKILL>7 THEN ADD=ADD+1000
760 FUEL=FUEL+300
790 IF FUEL>1000 THEN FUEL=1000
800 FOR I=1 TO ADD
810 SC=SC+1
820 LOCATE 8,34:PRINT SC;
830 NEXT
840 FOR I=0 TO 2000:NEXT:GOTO 270
849 ' Crash!
850 FOR I=1 TO 15:COLOR I
860 FOR DE=1 TO 100:NEXT:NEXT
870 COLOR 0:LOCATE 10,5
880 PRINT"Nice job, space dust!!"
890 FOR I=0 TO 3000:NEXT
900 OPEN"lander10.dat"FOR INPUT AS #1
910 INPUT #1,A:FOR I=0 TO A-1:INPUT #1,A$(I),S(I):NEXT
920 CLOSE #1:FOR I=0 TO A-1:IF SC>=S(I)THEN 940
930 NEXT:SC=0:X=0:ERASE LANDER:GOTO 10
939 ' High score
940 CLS:PRINT"Congratulations, you are one of the     all-time high scores!"
950 PRINT:PRINT"Enter your name or initials":INPUT A$
960 FOR G=A-1 TO I STEP-1:S(G+1)=S(G):A$(G+1)=A$(G):NEXT:S(I)=SC:A$(I)=A$
970 A=A+1:IF A>10 THEN A=10
980 OPEN"lander10.dat"FOR OUTPUT AS #1:PRINT#1,A;",";:FOR I=0 TO A-1:PRINT#1,A$(I);",";S(I):NEXT
990 CLOSE #1
1000 RUN
1009 ' Draw lander
1010 FOR X=3 TO 6:PSET(X,0),1:PSET(X,7),1:NEXT
1020 FOR Y=2 TO 4:PSET(1,Y),1:PSET(8,Y),1:NEXT
1030 PSET(2,1),1:PSET(7,1),1:PSET(2,5),1:PSET(7,5),1:PSET(3,6),1:PSET(6,6),1
1040 FOR X=0 TO 2:PSET(X,10),1:PSET(X+7,10),1:NEXT
1050 PSET(1,9),1:PSET(2,8),1:PSET(7,8),1:PSET(8,9),1
1060 GET(0,0)-(9,10),LANDER
1070 RETURN
1079 ' Display joystick X,Y
1080 PRINT STICK(0);STICK(1)
1090 GOTO 1080
2000 ' Check for Joystick model on disk
2005 ON ERROR GOTO 3000
2010 OPEN"i",1,"joystick.dat"
2020 INPUT#1,U,D,R,L
2030 CLOSE#1:ON ERROR GOTO 0
2040 DX=R-L:DY=D-U:MX=DX/2:MY=DY/2
2050 RX=DX*2:RY=DY*2:FL=1:RETURN
3000 IF ERR=53 THEN FL=0:RETURN

