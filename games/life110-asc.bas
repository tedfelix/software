1 ' Life v1.1
2 ' Copyright 2017, Ted Felix
3 ' File date: 1/3/1983 11:00:00 PM
5 DEFINT A-Z:DIM L$(22):DX=243:DY=243:STRIG ON
10 SCREEN 0:CLS:WIDTH 40:PRINT">> Life v1.1 <<"
20 PRINT"By: Ted Felix
25 INPUT"Instructions (Y/N)";A$
26 A$=LEFT$(A$,1):IF A$="y"OR A$="Y"THEN GOSUB 7000
30 INPUT"Screen size (1-40,1-22)";MX,MY
35 INPUT"Joystick (Y/N)";A$:A$=LEFT$(A$,1):IF A$="Y"OR A$="y"THEN JOYST=1
40 DIM SC(MX,MY),TE(MX,MY)
45 FOR Y=1 TO MY:FOR X=1 TO MX:SC(X,Y)=32:NEXT:NEXT
50 CLS:X=1:Y=1:LX=MX:LY=MY:GX=0:GY=0
60 PS=32:GOSUB 1000
70 GOSUB 2000:GEN=1
75 LOCATE 23,1:PRINT"Printout (Y/N)?";:A$=INPUT$(1):IF A$="Y"OR A$="y"THEN GOSUB 5000
80 LOCATE 23,1:PRINT">> Life <<     ";
90 LOCATE 24,1:PRINT"Generation:";GEN;
100 TEP=STRIG(0):TEP=STRIG(4):GOTO 3000
999 ' Set-up screen
1000 IF JOYST=1 THEN 1200
1002 A$=INKEY$:IF A$="" THEN 1002
1005 LOCATE Y,X:PRINT CHR$(PS);
1010 IF A$="8" THEN Y=Y-1:IF Y<1 THEN Y=1
1020 IF A$="2" THEN Y=Y+1:IF Y>MY THEN Y=MY
1030 IF A$="4" THEN X=X-1:IF X<1 THEN X=1
1040 IF A$="6" THEN X=X+1:IF X>MX THEN X=MX
1050 PS=SCREEN(Y,X):LOCATE Y,X:PRINT"Û";
1070 IF A$=CHR$(13) THEN LOCATE Y,X:PRINT CHR$(2);:PS=2:GOSUB 1500
1080 IF A$=" " THEN LOCATE Y,X:PRINT" ";:PS=32
1090 IF A$=CHR$(27) THEN IF PS<>2 THEN LOCATE Y,X:PRINT" ";:RETURN ELSE LOCATE Y,X:PRINT CHR$(2);:RETURN
1100 GOTO 1002
1199 ' Joystick
1200 LOCATE Y,X:PRINT CHR$(PS);
1210 X=STICK(0)*(MX/DX)+.5:IF X<1 THEN X=1 ELSE IF X>MX THEN X=MX
1220 Y=STICK(1)*(MY/DY)+.5:IF Y<1 THEN Y=1 ELSE IF Y>MY THEN Y=MY
1230 PS=SCREEN(Y,X):LOCATE Y,X:PRINT"Û";
1240 IF STRIG(5) THEN FOR I=1 TO 500:NEXT:IF PS=2 THEN LOCATE Y,X:PRINT CHR$(32);:PS=32 ELSE LOCATE Y,X:PRINT CHR$(2);:PS=2:GOSUB 1500
1250 IF STRIG(1) THEN IF PS<>2 THEN LOCATE Y,X:PRINT" ";:RETURN ELSE LOCATE Y,X:PRINT CHR$(2);:RETURN
1260 GOTO 1200
1499 ' Find where you put person
1500 IF X>GX THEN GX=X
1510 IF Y>GY THEN GY=Y
1520 IF X<LX THEN LX=X
1530 IF Y<LY THEN LY=Y
1540 RETURN
1999 ' Load screen into SC array
2000 LOCATE 23,1:COLOR 23:PRINT"Working";:COLOR 7
2005 FOR Y=LY TO GY:FOR X=LX TO GX
2010 SC(X,Y)=SCREEN(Y,X)
2200 NEXT:NEXT
2210 IF LY-1<1 THEN LY=1 ELSE LY=LY-1
2220 IF LX-1<1 THEN LX=1 ELSE LX=LX-1
2230 IF GX+1>MX THEN GX=MX ELSE GX=GX+1
2240 IF GY+1>MY THEN GY=MY ELSE GY=GY+1
2250 RETURN
2999 ' Life
3000 FOR Y=LY TO GY:FOR X=LX TO GX:NEI=0:FOR Y1=Y-1 TO Y+1:FOR X1=X-1 TO X+1:IF Y1<1 OR X1<1 OR X1>MX OR Y1>MY OR (X1=X AND Y1=Y) THEN ELSE IF SC(X1,Y1)=2 THEN NEI=NEI+1
3040 NEXT:NEXT
3050 IF NEI=3 THEN TE(X,Y)=2 ELSE IF NEI>3 OR NEI<2 THEN TE(X,Y)=32 ELSE IF NEI=2 THEN TE(X,Y)=SC(X,Y)
3060 NEXT:COLOR,,(MY-Y) MOD 16:NEXT
3299 ' Display new board
3300 PMY=GY:PMX=GX:PLY=LY:PLX=LX:LY=MY:LX=MX:GX=0:GY=0
3350 FOR Y=PLY TO PMY:FOR X=PLX TO PMX
3400 LOCATE Y,X:COLOR INT(RND*7)+1,0
3500 IF TE(X,Y)=0 THEN SC(X,Y)=32 ELSE SC(X,Y)=TE(X,Y)
3510 IF SC(X,Y)=2 AND Y<LY THEN LY=Y
3520 IF SC(X,Y)=2 AND X<LX THEN LX=X
3530 IF SC(X,Y)=2 AND X>GX THEN GX=X
3540 IF SC(X,Y)=2 AND Y>GY THEN GY=Y
3550 PRINT CHR$(SC(X,Y));
3600 NEXT:NEXT:COLOR 7,0
3700 GEN=GEN+1:LOCATE 24,1:PRINT"Generation:"GEN;
3799 ' Read keyboard
3800 A$=INKEY$
3810 IF A$="P"OR A$="p"THEN GOSUB 5000
3990 IF A$=CHR$(27) OR STRIG(0)THEN RUN
4000 IF A$=CHR$(13) OR STRIG(4)THEN GOSUB 1000:GOTO 70
4010 IF LY-1<1 THEN LY=1 ELSE LY=LY-1
4020 IF LX-1<1 THEN LX=1 ELSE LX=LX-1
4030 IF GX+1>MX THEN GX=MX ELSE GX=GX+1
4040 IF GY+1>MY THEN GY=MY ELSE GY=GY+1
4100 GOTO 3000
4999 ' Print
5000 LPRINT">> Life <<"
5005 LPRINT CHR$(27)"W"CHR$(1);:LOCATE 23,15:PRINT"*** Working ***";
5010 FOR Y=1 TO 22:L$(Y)=""
5020 FOR X=1 TO 40
5030 IF SC(X,Y)=2 THEN L$(Y)=L$(Y)+"*"ELSE L$(Y)=L$(Y)+" "
5040 NEXT:NEXT:LOCATE 23,15:PRINT"                  ";
5050 FOR L=1 TO 22:LPRINT L$(L);:NEXT
5060 LPRINT CHR$(27)"W"CHR$(0)
5070 LPRINT"Generation:";GEN
5080 LPRINT STRING$(80,"-")
5090 RETURN
6999 ' Instructions
7000 PRINT"Life"
7010 PRINT:PRINT"The life board is set-up by moving the
7020 PRINT"cursor with the cursor keys in the
7030 PRINT"Num-Lock state.  To put down a person,
7040 PRINT"press ENTER.  To kill a person, press
7050 PRINT"the space bar.  Press Esc to begin
7060 PRINT"Life.  While Life is working, you can
7070 PRINT"press `P' for a printout of the next
7080 PRINT"generation.  You can press ENTER to
7090 PRINT"change the screen after the next
7100 PRINT"generation or press Esc to restart the
7110 PRINT"program.  Be careful when putting down
7120 PRINT"people, if you put one too far out from
7130 PRINT"the center, Life will work slowly.
7140 PRINT:PRINT"Have fun."
7150 PRINT:PRINT:INPUT"Press ENTER to begin";A$
7160 RETURN

