4 ' FIGHTER
5 ' Written before 11-22-82
6 ' Copyright 2017, Ted Felix
7 ' File date: 10/13/1991 07:18:10 PM
10 CLEAR ,,2000
20 PLAY"mb"
100 CLS:SCREEN 1:KEY OFF:COLOR 0,0
200 '  Set-up graphics arrays
300 DIM TIE(6),CHA(4)
400 DRAW"BM1,0"
500 DRAW"G1D4F1BU3R2U1E1R2F1D2G1L2H1BR6BD2E1U4H1BD3L2
600 LOCATE 12
700 GET (0,0)-(10,6),TIE
800 PUT (0,0),TIE,XOR
900 DRAW"bm3,0D6U6D3L3R6"
1000 GET (0,0)-(6,6),CHA
1200 ' Set-up Random
1300 RANDOMIZE VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))+VAL(TIME$)
1400 ' Set-up Functions
1500 DEF FN RAND(X)=INT(RND*X)+1
1600 STRIG ON ' Joystick buttons
1700 ' Set-up Fighters
1800 FOR I=0 TO 2:F(I)=1:X(I)=FNRAND(309):Y(I)=FNRAND(193):PUT(X(I),Y(I)),TIE:NEXT
1900 GOSUB 7000:GOSUB 5000:GOSUB 6000
2000 GOSUB 7000:GOSUB 6000
2200 GOTO 1900
4999 ' Move fighters
5000 T=0:FOR I=0 TO 2
5005 GOSUB 6000
5006 'There's a bug in the next line which causes BASIC to use its
5007 'entire heap.  Can you find it boys and girls?
5010 IF F(I)=0 THEN T=T+1:IF T=3 THEN T=0:GOTO 1800 ELSE GOTO 5090
5015 PY=Y(I):PX=X(I):DX(I)=DX(I)+FNRAND(19)-10:DY(I)=DY(I)+FNRAND(19)-10
5020 X(I)=X(I)+DX(I)
5025 GOSUB 6000
5030 Y(I)=Y(I)+DY(I)
5040 IF X(I)<0 THEN F(I)=0:GOTO 5085
5045 GOSUB 6000
5050 IF X(I)>300 THEN F(I)=0:GOTO 5085
5060 IF Y(I)>183 THEN F(I)=0:GOTO 5085
5070 IF Y(I)<0 THEN F(I)=0:GOTO 5085
5075 GOSUB 6000
5080 PUT(X(I),Y(I)),TIE,XOR
5085 PUT(PX,PY),TIE,XOR
5090 NEXT:RETURN
6000 P1=X:P2=Y:X=(STICK(0)-3)*3.1:Y=(STICK(1)-3)*2:IF X>313 THEN X=313
6050 IF Y>190 THEN Y=190
6100 PUT(P1,P2),CHA,XOR:PUT(X,Y),CHA,XOR
6200 RETURN
7000 PUT(X,Y),CHA,XOR:LINE(0,199)-(X+3,Y+3),2
7002 FOR I=0 TO 2:IF F(I)=1 THEN PUT(X(I),Y(I)),TIE,XOR
7003 NEXT
7005 LINE(320,199)-(X+3,Y+3),2
7010 LINE(0,199)-(X+3,Y+3),0
7030 LINE(320,199)-(X+3,Y+3),0
7031 FOR I=0 TO 2:IF X(I)>X-9 AND X(I)<X+9 AND Y(I)>Y-6 AND Y(I)<Y+6 THEN GOSUB 8000
7032 NEXT
7040 PUT(X,Y),CHA,XOR
7050 FOR I=0 TO 2:IF F(I)=1 THEN PUT(X(I),Y(I)),TIE,XOR
7055 NEXT
7060 RETURN
7999 ' Kill
8000 F(I)=0:SOUND 1000,1:SC=SC+10:LOCATE 1,20:PRINT SC;:DX(I)=0:DY(I)=0
8010 FOR RAD=1 TO 10 STEP 2
8020 CIRCLE(X(I)+5,Y(I)+5),RAD,2
8030 NEXT RAD
8040 FOR RAD=1 TO 10 STEP 2
8050 CIRCLE(X(I)+5,Y(I)+5),RAD,0
8060 NEXT RAD
8090 RETURN
