1 ' Copyright 2017, Ted Felix
2 ' File date: 5/7/1989 09∶00∶22 PM
5 RANDOMIZE VAL(TIME$)+VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
10 CLS:SCREEN 0:WIDTH 40:KEY OFF
20 UN$=CHR$(24):DN$=CHR$(25):NO$=CHR$(14)
30 PR$=CHR$(1)
50 DEF FNRAND(X)=INT(X*RND)+1
100 SC=0:DE=0:CLS:PRINT"Ted
110 PRINT"Team
120 PRINT"----------N   N  III FFFFF EEEEE------
130 PRINT"         NN  N   I  F     E
140 PRINT"--------N N N   I  FFFF  EEEE---------
150 PRINT"       N  NN   I  F     E
160 PRINT"------N   N  III F     EEEEE----------
290 LOCATE 11,1:PRINT"Instructions?"
300 FOR I=1 TO 5000:A$=INKEY$:IF A$<>""THEN I=9000
305 NEXT:IF I<9001 THEN 320
310 IF A$="Y"OR A$="y"THEN 320 ELSE GOTO 900
320 LOCATE 11,1:PRINT SPACE$(20)
330 LOCATE 8,1
380 PRINT:PRINT"Use the joystick to move your man ("PR$")
390 PRINT"through the field of NIFEs ("UN$").
400 PRINT"Try to catch notes (";:COLOR 2,0:PRINT NO$;:COLOR 7,0:PRINT"), NIFEs that
410 PRINT"are pointing down (";:COLOR 3,0:PRINT DN$;:COLOR 7,0:PRINT") and letters.
420 PRINT:PRINT"Scoring:"
430 COLOR 3,0:PRINT DN$;:COLOR 7,0:PRINT" -  50
440 COLOR 2,0:PRINT NO$;:COLOR 7,0:PRINT" - 100
450 PRINT:PRINT"Letters:
460 PRINT"Catch the letters to spell ";:COLOR 4,0:PRINT"JAM SESSION";:COLOR 7,0:PRINT"."
470 PRINT"Each letter is worth 100 points except
480 PRINT"for S which is worth 300 points.
850 LOCATE 24,1:PRINT"Press any key to play NIFE!";
860 FOR I=1 TO 5000:A$=INKEY$:IF A$<>""THEN I=9000
870 NEXT:IF I<9001 THEN DE=1 ELSE DE=0
900 CLS:Y=1:IF DE=1 THEN X=20
910 ST$="JAM SESSION":LOCATE 25,7,0:PRINT ST$;
920 IF DE=1 THEN COLOR 0,7:LOCATE 25,20:PRINT"Press SPACE to play!";:COLOR 7,0
1000 IF DE=0 THEN X=STICK(0)/5 ELSE GOSUB 2500
1005 SY=CSRLIN
1010 LOCATE Y,X:PRINT PR$;
1090 EX=FNRAND(39):LOCATE 24,EX:PRINT UN$;
1100 IF FNRAND(50)=1 THEN LOCATE 24,EX:COLOR 3,0:PRINT DN$;:COLOR 7,0
1110 IF FNRAND(100)=1 THEN GOSUB 3000
1120 IF FNRAND(75)=1 THEN COLOR 2,0:PRINT NO$;:COLOR 7,0:PRINT UN$;
1190 SC=SC+1:LOCATE 25,1:PRINT SC;
1400 CH=SCREEN(Y+1,X)
1410 IF CH=24 THEN 2000
1420 IF CH=25 THEN SC=SC+50:GOTO 1900
1430 IF CH=14 THEN SC=SC+100:GOTO 1900
1490 Z$=CHR$(CH):GOSUB 3100
1900 LOCATE SY,1:PRINT:GOTO 1000
1999 ' Boom!
2000 IF DE=1 THEN 100
2010 CLS:PRINT"Boom!":PRINT"Score:"SC
2020 PRINT:COLOR 23,0:PRINT"GAME OVER":COLOR 7,0
2030 FOR I=1 TO 5000:NEXT
2040 GOTO 100
2499 ' Demo
2500 X=X+FNRAND(5)-3
2510 IF X<1 THEN X=1
2520 IF X>39 THEN X=39
2525 A$=INKEY$:IF A$<>""THEN 100
2530 IF SCREEN(Y+1,X)=24 THEN 2550
2535 IF SC>300 THEN RETURN 100
2540 RETURN
2550 D=FNRAND(2)
2560 IF D=1 THEN X=X+1 ELSE X=X-1
2570 IF X<1 THEN X=2 ELSE IF X>39 THEN X=38
2580 RETURN
2999 ' JAM SESSION Letter picker
3000 L$=MID$(ST$,FNRAND(11),1)
3010 LOCATE 24,EX:COLOR 4,0:PRINT L$:COLOR 7,0:RETURN
3099 ' JAM SESSION decoder
3100 IF Z$>="A"AND Z$<="S" THEN 3120
3110 RETURN
3120 FOR I=1 TO 11
3130 IF MID$(ST$,I,1)=Z$THEN MID$(ST$,I,1)=" ":SC=SC+100
3140 NEXT
3150 LOCATE 25,7:PRINT ST$;
3160 IF ST$=STRING$(11," ")THEN 4000:RETURN 910
3170 RETURN
3999 ' Jam Session
4000 CLS:SCREEN 1:FOR I=1 TO 200:COLOR I MOD 8:NEXT
4010 PLAY"mb
4020 FOR I=1 TO 20:SOUND FNRAND(300)+37,FNRAND(15):NEXT
4030 SCREEN 0:LOCATE 1,19,0:PRINT"³  ³":COLOR 22,4:LOCATE 2,19:PRINT"Ted "
4040 LOCATE 3,19:PRINT"Team":COLOR 7,0
4050 FOR I=1 TO 400:COLOR ,,I MOD 16:NEXT
5000 PRINT"               ___
5010 PRINT"                ³ /O _ ___
5020 PRINT"                ³ \³/oO ³
5030 PRINT"               / \Team / \
5040 PRINT"              ÄÄÄÂÄÄÄÄÂÄÄÄ
5050 PRINT"                 ³    ³
5060 PRINT"                 ³    ³
5070 PRINT"              Guy Guy Gary
5080 PRINT"   O      __O
5090 PRINT"   /\     \_\                       (³
5100 PRINT"   ³        ³                   O  ()³
5110 PRINT"  / \      / \     O   ÚÄ¿      ³/()&³
5120 PRINT"  V V    Blue      ³/ÚÄÙ ³      ³³   ³
5130 PRINT"          Boo      ³ ³   ³      ³³   ³
5140 PRINT"           Boo    / \³   ³     / \   ³
5150 PRINT"                      Ted      Southman
5160 FOR I=1 TO 300:COLOR,,I MOD 16:NEXT
5170 COLOR,,0
5180 X=2000
5190 SOUND X,.01:IF X<38 THEN 5210
5200 X=X-1:GOTO 5190
5210 OUT &H3D4,8:OUT &H3D5,1
5220 FOR B=1 TO 7:SOUND 2000,1:SOUND 32767,2:FOR I=1 TO 3:SOUND 1900,1:SOUND 32767,2:NEXT:NEXT
5230 FOR I=1 TO 5:SOUND 1900,1:SOUND 32767,1:NEXT
5240 SOUND 32767,1:SOUND 1900,1
5250 FOR I=1000 TO 500 STEP-1:SOUND I,.01:NEXT
5260 FOR I=1 TO 200:SOUND FNRAND(2000)+37,.01:NEXT
5270 FOR I=1 TO 50:SOUND 100+I,2:SOUND 200+I,2:NEXT
5280 X=200+I:FOR I=X TO 37 STEP-1:SOUND I,1:NEXT
5300 OUT &H3D4,8:OUT &H3D5,2
5320 FOR O=0 TO 170 STEP 20:FOR I=1000 TO 1100 STEP 10:SOUND I+O,1:NEXT:NEXT
5330 FOR I=0 TO 40
5340 SOUND FNRAND(200)+37,2
5350 SOUND 32767,1:PRINT:NEXT
5500 RETURN

