1 ' Copyright 2017, Ted Felix
5 DEF SEG=0:RANDOMIZE (PEEK(&H46C)+PEEK(&H46D)*256)-32768!
10 DEF FNRAND(X)=INT(RND*X)+1
20 DY(1)=-1:DY(2)=1:DX(3)=-1:DX(4)=1
30 DY(3)=0:DY(4)=0:DX(1)=0:DX(2)=0:WI=40
40 W$=STRING$(WI-1,219)
50 PR$=CHR$(1)
1000 IV=1:GOSUB 8000:YY=2:YX=2:LOCATE YY,YX:PRINT PR$;
1100 A$=INKEY$:IF A$=""THEN 1300
1110 PX=YX:PY=YY:R$=RIGHT$(A$,1)
1120 IF R$="H"THEN YY=YY-1
1130 IF R$="K"THEN YX=YX-1
1140 IF R$="M"THEN YX=YX+1
1150 IF R$="P"THEN YY=YY+1
1160 LOCATE PY,PX:PRINT" ";
1170 SC=SCREEN(YY,YX):IF SC<>32 THEN YY=PY:YX=PX
1180 LOCATE YY,YX:PRINT PR$;
1190 IF YY=22 AND YX=WI-2 THEN 1500
1300 LOCATE 24,1:PRINT TIME$;
1310 GOTO 1100
1500 LOCATE 24,1:PRINT"You made it!!!  Play again?";
1510 A$=INPUT$(1):IF A$="Y"OR A$="y"THEN RUN
1520 CLS:PRINT"Bye!":PRINT:PRINT:END
8000 SCREEN 0,0,0,0:CLS:WIDTH WI:SCREEN 0,0,IV,IV:X=WI-2:Y=22
8080 LOCATE 1,1:PRINT"Please wait while I generate the maze!"
8090 SCREEN ,,0,IV
8100 CLS:FOR I=1 TO 23:PRINT W$:NEXT
8110 LOCATE Y,X:PRINT"@";
8120 IF Y>2 THEN DU=SCREEN(Y-2,X)ELSE DU=10
8130 IF Y<22 THEN DD=SCREEN(Y+2,X)ELSE DD=10
8140 IF X>2 THEN DL=SCREEN(Y,X-2)ELSE DL=10
8150 IF X<78 THEN DR=SCREEN(Y,X+2)ELSE DR=10
8160 IF DU<219 AND DD<219 AND DR<219 AND DL<219 THEN 8300
8165 CO=0:A(1)=0:A(2)=0:A(3)=0:A(4)=0
8170 IF DU=219 THEN A(1)=-1:CO=CO+1
8180 IF DD=219 THEN A(2)=-1:CO=CO+1
8190 IF DL=219 THEN A(3)=-1:CO=CO+1
8200 IF DR=219 THEN A(4)=-1:CO=CO+1
8210 S=FNRAND(4):IF A(S)=0 THEN 8210
8230 X=X+DX(S):Y=Y+DY(S):LOCATE Y,X:PRINT" ";
8240 X=X+DX(S):Y=Y+DY(S):LOCATE Y,X:PRINT CHR$(63+S);:GOTO 8120
8300 SC=SCREEN(Y,X)-63:LOCATE Y,X:PRINT" ";:IF SC<1 THEN 8600
8310 X=X-DX(SC)*2:Y=Y-DY(SC)*2
8320 GOTO 8120
8600 SCREEN ,,0,0:RETURN

