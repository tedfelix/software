1 ' Copyright 2017, Ted Felix
2 ' File data: 4/12/1987 01:02:16 AM
100 DEF FNRAND(X)=INT(RND*X)+1
200 OM=1:SP=.5:SHIP$="/^\":P=1:FC=5
300 INPUT"Do you have a joystick";A$
400 A$=LEFT$(A$,1):IF A$="y" OR A$="Y" THEN JOY=1:GOSUB 7000 ELSE JOY=0
500 STRIG ON:KEY OFF:SCREEN 0:WIDTH 40:CLS
600 Y=1:X=FNRAND(30)+4
700 COLOR 2,0:LOCATE 25,1,0:PRINT STRING$(39,"~");:COLOR 7,0
710 FOR I=1 TO 5
720 A=FNRAND(30)+4:IF SCREEN (25,A)<>126 THEN 720
730 LOCATE 25,A:PRINT"I";
740 NEXT
800 F$="º"+CHR$(30)+CHR$(29):F$=F$+F$
900 M$=F$+F$+F$+F$+F$+F$+F$+F$+F$+F$+F$
1000 F$=" "+CHR$(30)+CHR$(29):F$=F$+F$
1100 S$=F$+F$+F$+F$+F$+F$+F$+F$+F$+F$+F$
1200 IF JOY THEN M=INT(STICK(0)/XS3)-1:LOCATE 1,1:IF OH=0 THEN F=STICK(1):IF F<20 OR STRIG(1) THEN 2400
1300 IF OH THEN IF H<10 THEN OH=0
1400 A$=INKEY$:IF LEN(A$)=2 THEN A$=RIGHT$(A$,1):IF A$="K" THEN M=-1 ELSE IF A$="M" THEN M=1 ELSE IF A$="H" AND OH=0 THEN 2400
1500 PR=P:P=P+M:IF P>36 THEN P=36 ELSE IF P<2 THEN P=2
1600 LOCATE 24,PR:PRINT"   ";:IF SCREEN(25,P)=42 OR SCREEN(25,P+1)=42 OR SCREEN(25,P+2)=42 THEN 3800
1700 LOCATE 24,P:PRINT SHIP$;
1800 IF NOT JOY THEN M=0
1900 LOCATE 1,1:PRINT"Temp ";USING"##.#";H
2000 LOCATE 1,24:PRINT"Score ";SC
2050 LOCATE 1,34:PRINT"FC ";FC
2100 IF OH THEN H=H-.05:COLOR 20,0:LOCATE 1,12:PRINT"-Overheated-":COLOR 7,0 ELSE LOCATE 1,12:PRINT"            ";
2200 H=H-.05:IF H<-9 THEN H=-9
2300 GOSUB 2900:GOTO 1200
2400 COLOR 4,0:IF JOY THEN LOCATE 23:PRINT SPACE$(P)+M$;:LOCATE 22:PRINT SPACE$(P)+S$;
2500 IF JOY=0 THEN LOCATE 22:PRINT SPACE$(P)+M$;:LOCATE 22:PRINT SPACE$(P)+S$;
2600 COLOR 7,0:IF INT(P+.5)=X-1 THEN 4100
2700 H=H+.5:IF H>20 THEN OH=1
2800 GOTO 1200
2900 PRE=Y:Y=Y+SP
3000 LOCATE PRE,X:PRINT" ";
3100 IF Y>=24 THEN Y=1:GOTO 3600
3200 X=X+FNRAND(3)-2:IF X<2 THEN X=2
3300 IF X>38 THEN X=38
3400 COLOR 6,0:LOCATE Y,X
3410 IF OM THEN PRINT"ê"; ELSE PRINT"à";
3420 COLOR 7,0
3500 RETURN
3600 IF OM=0 THEN 5000
3680 A=SCREEN(25,X):IF A=73 THEN FC=FC-1
3690 LOCATE 25,X:PRINT"*";:GOSUB 3900
3700 SC=SC-1:X=FNRAND(30)+4:GOTO 2900
3800 P=PR:GOTO 1700
3900 IF SCREEN(25,P)=42 OR SCREEN(25,P+2)=42 THEN 5000
3950 IF SP>.9000001 THEN IF FNRAND(10)=1 THEN OM=0
4000 RETURN
4100 FOR I=0 TO 50
4105 COLOR FNRAND(15),FNRAND(8)
4110 LOCATE Y,X:PRINT CHR$(FNRAND(75)+179);
4120 NEXT
4130 COLOR 7,0:LOCATE Y,X:PRINT" ";
4190 SP=SP+.05:X=FNRAND(30)+4
4200 Y=1:IF OM=1 THEN SC=SC+2
4210 IF OM=0 THEN SC=SC+10
4300 OM=1:GOTO 2700
5000 FOR I=1 TO 15:COLOR 7,I,I
5010 CLS:FOR D=0 TO 100:NEXT:NEXT
5020 COLOR 7,0,0:CLS:LOCATE 12
5030 PRINT"Nice job! The area will now"
5040 PRINT"be destroyed by Alphan Warriors"
5050 FOR I=0 TO 2000:NEXT
5060 L$=STRING$(39,"~"):A$=STRING$(39,"à")
5070 FOR I=0 TO 10:LOCATE 25,1:PRINT A$;:LOCATE 25,1
5080 COLOR 2,0:PRINT L$;:COLOR 6,0:NEXT
5090 CLS:LOCATE 25,1:PRINT A$;:COLOR 7,0
5100 LOCATE 12:PRINT"Too bad!"
5200 FOR I=1 TO FC:LOCATE 10,I
5300 FOR D=1 TO 300:NEXT:PRINT"I";
5400 LOCATE 11,1:PRINT I*10:NEXT:SC=SC+FC*10
5500 PRINT"Score =";SC:PRINT:PRINT
6900 END
6999 ' Read joystick model
7000 OPEN"joystick.dat"FOR INPUT AS #1
7010 INPUT #1,U,D,R,L
7020 CLOSE #1
7030 XS3=(L+R)/3
7040 RETURN
