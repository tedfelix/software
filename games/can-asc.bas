1 ' Copyright 2017, Ted Felix
2 ' File date: 11/28/1982 11:00:00 PM
5 RANDOMIZE VAL(TIME$)+VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
10 CLS:SCREEN 1:COLOR 0,0:KEY OFF
15 DEF FNRAND(X)=INT(RND*X)+1
20 DEF FNRAD(DEG)=DEG*.0174532
25 DEF FNDEG(RAD)=RAD/.0174532
30 DEF FNARCSIN(X)=ATN(X/SQR(-X*X+1))
35 DEF FNARCCOS(X)=-ATN(X/SQR(-X*X+1))+1.5708
40 DEF FNSIND(X)=SIN(FNRAD(X))
45 DEF FNCOSD(X)=COS(FNRAD(X))
50 STRIG ON
60 DIM XC(19),YC(19)
70 PRINT TAB(17)"Cannon"
80 PRINT TAB(13);"By: Ted Felix"
99 ' Compute cannon positions
100 FOR I=1 TO 19
110 D=I*4.73685
120 XC(I)=FNSIND(D)*10
130 YC(I)=200-(FNCOSD(D)*15)
140 NEXT
199 ' Get angle and force
200 CLS:LINE(319,199)-(300,179),,BF
205 PY=Y:X=STICK(0):Y=STICK(1)
207 IF Y>100 THEN Y=100
210 D=Y*.9650001
215 F=X/50
220 LOCATE 1,1:PRINT"D"INT(D)TAB(7)"F"F TAB(17)"A"0 TAB(25)"Di"310
230 LINE (0,199)-(XC(PY*.19),YC(PY*.19)),0
240 LINE (0,199)-(XC(Y*.19),YC(Y*.19)),1
250 X1=XC(Y*.19):Y1=YC(Y*.19)
390 IF STRIG(1)=0 THEN 205
399 ' Compute velocitys
400 D=INT(D):DX=FNSIND(D)*F
410 DY=FNCOSD(D)*F
420 GRAV=3.000001E-03:Y=Y1:X=X1:PY=Y:PX=X
490 LINE(300,199)-(319,199)
499 ' Move ball
500 PSET(PX,PY),2:PSET(INT(X),INT(Y)):PX=INT(X):PY=INT(Y)
510 X=X+DX:Y=Y-DY
520 DY=DY-GRAV
530 IF POINT(INT(X),INT(Y))=3 AND X>298 THEN 600
560 IF X>319 THEN 600
570 IF Y>199 THEN 600
580 AL=INT(199-Y):DI=INT(300-X):LOCATE 1,18:PRINT AL:LOCATE 1,27:PRINT DI
590 GOTO 500
599 ' Done moving
600 IF X>319 THEN PRINT"Overshot.  ";:GOTO 700
610 IF Y>199 THEN GOSUB 900
620 IF X>299 THEN GOSUB 800:GOTO 700
630 PRINT"You missed.  ";
700 PRINT"Press Button to play again."
710 IF STRIG(1)=0 THEN 710
720 Y=0:LOCATE 2,1:PRINT SPACE$(49);
725 LOCATE 3,1:PRINT SPACE$(49);
730 FOR I=0 TO 2000:NEXT:CLS:GOTO 200
799 ' Win!
800 IF X>307 AND X<313 THEN PRINT"Direct hit!!!" ELSE PRINT"Not a bad shot!!"
810 FOR I=1 TO 10
820 LINE(300,179)-(319,199),0,BF
830 LINE(300,179)-(319,199),,BF:NEXT
840 FOR Y=179 TO 198:FOR X=300 TO 319
850 PRESET(X,Y):NEXT:NEXT
890 RETURN
899 ' Explosion
900 W=DY*-20:H=DY*-8
910 WI=(W-1)/2
920 FOR I=1 TO 40
930 LINE(X,Y)-(FNRAND(W)-WI+X,FNRAND(H)+Y-H):NEXT
990 RETURN
999 ' Analysis
1000 CLS:SCREEN 0:KEY ON
1010 PRINT"Cannon shot analysis."
1020 PRINT:PRINT"Position:"STR$(D)"ø"TAB(17)"Force:";F
1030 PRINT"Final altitude:";AL
1040 PRINT"Final distance:";
1050 IF DI<0 THEN PRINT" 0"ELSE PRINT DI
