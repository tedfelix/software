1 ' Copyright 2017, Ted Felix
5 RANDOMIZE TIMER
10 COLOR 2:CLS:SCREEN 0:WIDTH 80:KEY OFF
20 ALP$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
100 READ MAX:DIM T$(MAX),A$(MAX),U(MAX)
110 FOR I=1 TO MAX:READ T$(I),A$(I):NEXT
1000 CLS
1005 C=INT(RND*MAX)+1:IF U(C)=0 THEN U(C)=-1 ELSE 1005
1007 T$=T$(C):A$=A$(C):A2$=A$
1010 FOR I=1 TO LEN(A$):CH$=MID$(A$,I,1):IF CH$<>" "THEN IF CH$<"A"THEN COLOR 18:PRINT CH$;:COLOR 2:MID$(A2$,I)=" " ELSE PRINT"þ"; ELSE PRINT" ";
1020 NEXT
1030 PRINT:PRINT:PRINT T$:LOCATE 25,1:PRINT ALP$;
1040 SCORE=0:LOCATE 23,60:PRINT"Score: 0"
1100 LOCATE 10,1:LINE INPUT"Enter a letter? ";G$:IF LEN(G$)=1 THEN IF G$>"Z"THEN G$=CHR$(ASC(G$)-32) ELSE ELSE IF G$=A$ THEN S$=G$:GOTO 1360 ELSE GOSUB 2000:LOCATE 11,1:PRINT"Wrong guess":GOTO 1100
1102 GOSUB 2000
1105 G=ASC(G$)-64:IF SCREEN(25,G)=32 THEN LOCATE 11,1:PRINT"Already guessed":GOTO 1100
1110 LOCATE 25,G:PRINT" ";:LOCATE 11,1:F=0:ST=1:SC=0
1120 A=INSTR(ST,A$,G$):IF A=0 THEN 1150
1130 F=1:SC=SC+1:IF A<81 THEN LOCATE 1,A ELSE LOCATE 2,A-80
1140 PRINT G$;:MID$(A2$,A)=" ":ST=A+1:GOTO 1120
1150 IF F=0 THEN PRINT"No "G$"s":GOTO 1100
1160 LOCATE 12,1:PRINT SC"of those!":FOR I=1 TO 1000:NEXT:LOCATE 12:PRINT SPACE$(20)
1170 SCORE=SCORE+SC:LOCATE 23,60:PRINT"Score:";SCORE:GOSUB 1300
1180 GOTO 1100
1299 ' Figure out if there are only vowels left
1300 F=0:FOR I=1 TO LEN(A2$):CH$=MID$(A2$,I,1):IF CH$=" "THEN 1320
1310 IF CH$<>"A"AND CH$<>"E"AND CH$<>"I"AND CH$<>"O"AND CH$<>"U"THEN F=1:I=200
1320 NEXT:IF F=1 THEN RETURN ELSE RETURN 1330
1330 BEEP
1340 LOCATE 15,1:PRINT"You must now solve the puzzle, all letters are vowels"
1350 INPUT"The solution is";S$
1360 IF S$=A$ THEN COLOR 18:PRINT">>> RIGHT <<<"SPACE$(20);:LOCATE 1,1:PRINT A$;:COLOR 2:LOCATE 18,1:FOR I=1 TO 2500:NEXT:GOTO 1000
1370 PRINT"Wrong!":GOTO 1100
1390 END
1999 ' Routine to clear lines 10 and 11
2000 LOCATE 10,1:PRINT SPACE$(79):PRINT SPACE$(79):RETURN
9000 DATA 21
9010 DATA A Phrase,THE RAIN IN SPAIN FALLS MAINLY IN THE PLANE
9020 DATA Things,SPAGHETTI AND MEATBALLS
9030 DATA A Phrase,TIME IS OF THE ESSENCE
9040 DATA A Person,SANTA CLAUS
9050 DATA A Phrase,AN EYE FOR AN EYE A TOOTH FOR A TOOTH
9060 DATA A Thing,IBM PERSONAL COMPUTER
9070 DATA A Thing,PLAYER PIANO
9080 DATA A Thing,CARDBOARD BOX
9090 DATA A Person,JOHN MCENROE
9100 DATA A Thing,TELEVISION SET
9110 DATA A Group,TED TEAM
9120 DATA Famous Saying,THE ONLY WAY TO A MAN'S STOMACH IS THROUGH HIS MOUTH
9130 DATA A Cat,GARFIELD
9140 DATA Famous Saying,HOW MANY LICKS DOES IT TAKE TO GET TO THE CENTER OF A TOOTSIE POP
9150 DATA A Person,BILL COSBY
9160 DATA A Thing,CHAIN SAW
9170 DATA A Thing,ROTO-TILLER
9180 DATA A Thing,HAUNTED HOUSE
9190 DATA A Game,CHALLENGE YAHTZEE
9200 DATA A Group,RIVERSIDE CONNECTION
9210 DATA A Quote,"FRIENDS, ROMANS, AND COUNTRYMEN LEND ME YOUR EARS"

